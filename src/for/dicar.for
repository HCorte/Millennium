C  GXSRC:DICAR.FOR
C
C V01 29-MAR-2006 FRP IPS Distribution release.
C
C SUBROUTINE TO DECODE INSTANT CAR MESSAGE FROM TERMINAL
C
C+++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
C This item is the property of GTECH Corporation, Providence, Rhode
C Island, and contains confidential and trade secret information. It
C may not be transferred from the custody or control of GTECH except
C as authorized in writing by an officer of GTECH. Neither this item
C nor the information it contains may be used, transferred,
C reproduced, published, or disclosed, in whole or in part, and
C directly or indirectly, except as expressly authorized by an
C officer of GTECH, pursuant to written agreement.
C
C Copyright 1997 GTECH Corporation. All rights reserved.
C+++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
C
C=======OPTIONS /CHECK=NOOVERFLOW
	SUBROUTINE DICAR(TERMES,TRABUF,MESLEN,AGTPAS,SALPAS)
	IMPLICIT NONE
C
	INCLUDE 'INCLIB:SYSPARAM.DEF'
	INCLUDE 'INCLIB:SYSEXTRN.DEF'
	INCLUDE 'INCLIB:GLOBAL.DEF'
	INCLUDE 'INCLIB:CONCOM.DEF'
	INCLUDE 'INCLIB:AGTCOM.DEF'
	INCLUDE 'INCLIB:DESTRA.DEF'
	INCLUDE 'INCLIB:TASKID.DEF'
	INCLUDE 'INCLIB:CHKSUMCM.DEF'
C
	INTEGER*2 MESLEN
	BYTE	  TERMES(*)
C
	INTEGER*4   TEMP
	INTEGER*4   CHKLEN, MYCHKSUM, ENCMES, ENCACT
	INTEGER*4   OPTIONS, SALPAS, AGTPAS, IND
C
C GET SEQUENCE NUMBER
C
	TEMP = ZEXT(TERMES(1))
	TRABUF(TTRN)=IAND(TEMP,15)
C
C GET CHECKSUM
C
	CALL TERM_TO_HOST(TERMES(3), TRABUF(TCHK), 2)
C
C GET STATISTICS
C
 	TRABUF(TTSTCS)=ZEXT(TERMES(5))
C
C GET OPTION FLAGS
C
	CALL TERM_TO_HOST(TERMES(6), OPTIONS, 2)
        IND=8
C
C CHECK FOR NODE NUMBER (NOT USED)
C
        IF(IAND(OPTIONS,'0001'X).NE.0) THEN
           IND=IND+4
        ENDIF
C
C CHECK FOR RETAILER NUMBER (NOT USED)
C
        IF(IAND(OPTIONS,'0002'X).NE.0) THEN
           IND=IND+4
        ENDIF
C
C CHECK FOR PASSWORD (NOT USED)
C
        IF(IAND(OPTIONS,'0004'X).NE.0) THEN
           IND=IND+2
        ENDIF
C
C CHECK FOR ORIGINATOR   (NOT USED)
C
        IF(IAND(OPTIONS,'0008'X).NE.0) THEN
           IND=IND+2
        ENDIF
C
C CHECK FOR LOCATION NUMBER (NOT USED)
C
        IF(IAND(OPTIONS,'0010'X).NE.0) THEN
           IND=IND+4
        ENDIF
C
C CHECK FOR USER ID (NOT USED)
C
        IF(IAND(OPTIONS,'0020'X).NE.0) THEN
           IND=IND+4
        ENDIF
C
C CHECK FOR OPERATOR ID (NOT USED)
C
        IF(IAND(OPTIONS,'0040'X).NE.0) THEN
           IND=IND+1
        ENDIF
C
C CHECK FOR PAYMENT TYPE (NOT USED HERE)
C
        IF(IAND(OPTIONS,'0080'X).NE.0) THEN
           IND=IND+2
        ENDIF
C
C CHECK FOR VALIDATION MODE (NOT USED HERE)
C
        IF(IAND(OPTIONS,'0100'X).NE.0) THEN
           IND=IND+2
        ENDIF
C
C CHECK FOR BANK (NOT USED HERE)
C
        IF(IAND(OPTIONS,'0200'X).NE.0) THEN
           IND=IND+8
        ENDIF
C
C GET AGENT PASS CODE
C
	CALL TERM_TO_HOST(TERMES(IND), AGTPAS, 2)
	IND=IND+2
C
C GET SALES REP NUMBER
C
	CALL TERM_TO_HOST(TERMES(IND), TRABUF(TCREP), 4)
	IND=IND+4
C
C GET SALES REP PASS CODE
C
	CALL TERM_TO_HOST(TERMES(IND), SALPAS, 2)
	IND=IND+2
C
C GET CLASS
C
	TRABUF(TCCLS) = ZEXT(TERMES(IND))
	IND=IND+1
C
        IF(TRABUF(TCCLS).NE.1) THEN
           TRABUF(TERR)=SYNT
           SYNTERRCOD=60
           GOTO 8000
        ENDIF
C
C GET GAME NUMBER
C
        CALL TERM_TO_HOST(TERMES(IND), TRABUF(TCGAM), 2)
	IND=IND+2
C
C GET CARTON NUMBER
C
	CALL TERM_TO_HOST(TERMES(IND), TRABUF(TCCAR), 4)
	IND=IND+4
C
C GET START AND END PACK NUMBERS
C
	CALL TERM_TO_HOST(TERMES(IND), TRABUF(TCSTA), 4)
	IND=IND+4
C
	CALL TERM_TO_HOST(TERMES(IND), TRABUF(TCEND), 4)
	IND=IND+4
C
C       IF(AGTPAS.EQ.0.OR.TRABUF(TCREP).EQ.0.OR.SALPAS.EQ.0) THEN
        IF(AGTPAS.EQ.0) THEN
           TRABUF(TERR)=SYNT
           SYNTERRCOD=61
           GOTO 8000
        ENDIF
C
C CHECK MESSAGE CHECKSUM
C
	IF(P(SUPSUM).EQ.0) THEN
          IF(.NOT.BTEST(AGTTAB(AGTTYP,TRABUF(TTER)),AGTSUM)) THEN
	    I4CCITT=IAND(BASECHKSUM+TRABUF(TTER),'FFFF'X)
	    CALL HOST_TO_TERM(TERMES(3), I4CCITT, 2)
	    CHKLEN=MESLEN-1
	    CALL GETCCITT(TERMES,1,CHKLEN,MYCHKSUM)
	    IF(MYCHKSUM.NE.TRABUF(TCHK)) TRABUF(TERR)=CBAD
	  ENDIF
	ENDIF
C
C CHECK FOR DES ERROR
C
	IF(P(DESACT).EQ.0) THEN
	  ENCMES = ZEXT(TERMES(1))
	  ENCMES = IAND(ENCMES,'08'X)
	  IF(P(DESFLG).EQ.0.AND.
     *	     BTEST(AGTTAB(AGTTYP,
     *            TRABUF(TTER)),AGTDES)) THEN
	    ENCACT='08'X
	  ELSE
	    ENCACT=0
	  ENDIF
	  IF(ENCMES.NE.ENCACT) TRABUF(TERR) = DESMOD
	ENDIF
C
8000	CONTINUE
	IF(TRABUF(TERR).NE.NOER) TRABUF(TSTAT)=REJT
	RETURN
	END
