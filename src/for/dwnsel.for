C
C PROGRAM DWNSEL
C $Log:   GXAFXT:[GOLS]DWNSEL.FOV  $
C  
C     Rev 1.0   17 Apr 1996 13:03:14   HXK
C  Release of Finland for X.25, Telephone Betting, Instant Pass Thru Phase 1
C  
C     Rev 1.0   21 Jan 1993 16:11:26   DAB
C  Initial Release
C  Based on Netherlands Bible, 12/92, and Comm 1/93 update
C  DEC Baseline
C
C ** Source - dwnsel.for **
C
C DWNSEL.FOR
C
C V02 07-OCT-91 MTK INITAL RELEASE FOR NETHERLANDS
C V01 01-AUG-90 XXX RELEASED FOR VAX
C
C
C DAILY WINNER SELECTION PROGRAM
C
C
C
C
C+++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
C This item is the property of GTECH Corporation, Providence, Rhode
C Island, and contains confidential and trade secret information. It
C may not be transferred from the custody or control of GTECH except
C as authorized in writing by an officer of GTECH. Neither this item
C nor the information it contains may be used, transferred,
C reproduced, published, or disclosed, in whole or in part, and
C directly or indirectly, except as expressly authorized by an
C officer of GTECH, pursuant to written agreement.
C
C Copyright 1991 GTECH Corporation. All rights reserved.
C+++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
C
C=======OPTIONS /CHECK=NOOVERFLOW
	PROGRAM DWNSEL
	IMPLICIT NONE
C
	INCLUDE 'INCLIB:SYSPARAM.DEF'
	INCLUDE 'INCLIB:SYSEXTRN.DEF'
	INCLUDE 'INCLIB:GLOBAL.DEF'
	INCLUDE 'INCLIB:CONCOM.DEF'
	INCLUDE 'INCLIB:DESTRA.DEF'
	INCLUDE 'INCLIB:DESLOG.DEF'
	INCLUDE 'INCLIB:PRMLOG.DEF'
	INCLUDE 'INCLIB:VALFIL.DEF'
	INCLUDE 'INCLIB:HSHCOM.DEF'
	INCLUDE 'INCLIB:DWNCOM.DEF'
C
        INTEGER*4 TYPE, EOF, IND, BLOCK, LENGTH, SAVE, I, K, VLFCNT
        INTEGER*4 ST, TUBSIZ, VSIZE, WIN, TST, DSIZE, FILCNT, DCFCNT
	INTEGER*4 DUMMY
	PARAMETER (DSIZE=4000000)
	PARAMETER (VSIZE=2000000)
	PARAMETER (TUBSIZ=I4BUCSIZ*7)
	INTEGER*4 BIGDCF(DSIZE),BIGVLF(VSIZE),FILES(6,20)
	INTEGER*4 REDBUF(TUBSIZ),VLFBUF(TUBSIZ),DCFBUF(TUBSIZ)
	INTEGER*4 TMFBUF(8192),LOGBUF(LREC*3),TFDB(7),TEMP
	BYTE I1TEMP(4)
	EQUIVALENCE (TEMP,I1TEMP)
C
C
	DATA EOF/0/
	DATA VLFCNT/0/
	DATA DCFCNT/0/
C
C
C
	CALL COPYRITE
	CALL SNIF_AND_WRKSET
C
C LOAD WINNER SELECTION COMMON AND COPY VLF FILE
C
	CALL DWNINT(FILES,FILCNT)
	CALL COPVLF
C
C OPEN FILES
C
	CALL IOPEN(SFNAMES(1,VLF),VLF,VFLEN*2,VFSCDC,VFSSER*2-1,ST)
	IF(ST.NE.0) CALL FILERR(SFNAMES(1,VLF),1,ST,0)
C
C
	CALL IOPEN(SFNAMES(1,DCF),DCF,LREC*2,LCDC,LSER*2-1,ST)
	IF(ST.NE.0) CALL FILERR(SFNAMES(1,DCF),1,ST,0)
	CALL ITUBSIZE(DCF,TUBSIZ)
C
C
	CALL IOPEN(SFNAMES(1,VLC),VLC,VFLEN*2,VFSCDC,VFSSER*2-1,ST)
	IF(ST.NE.0) CALL FILERR(SFNAMES(1,VLC),1,ST,0)
	CALL ITUBSIZE(VLC,TUBSIZ)
	CALL IINIB(VLC,BIGVLF,VSIZE)
C
C
	CALL IOPEN(SFNAMES(1,DCC),DCC,LREC*2,LCDC,LSER*2-1,ST)
	IF(ST.NE.0) CALL FILERR(SFNAMES(1,DCC),1,ST,0)
	CALL ITUBSIZE(DCC,TUBSIZ)
C
C CARRYOVER FILE PROCESSING.
C
	TYPE*,IAM(),' Carryover file processing started'
	CARRYSCAN=.TRUE.
200	CONTINUE
	WIN=0
	CALL ISREAD(LOGBUF,DCF,REDBUF,ST)
	IF(ST.EQ.ERREND) GOTO 300
	IF(ST.NE.0) CALL FILERR(SFNAMES(1,DCF),2,ST,0)
	CALL LOGTRA(TRABUF,LOGBUF)
	IF(TRABUF(TGAMTYP).EQ.TNBR) CALL NIFWIN(TRABUF,WIN)
C
C IF TRANSACTION IS A WINNER THEN CHECK
C IF IT WON ON A PREVIOUS DRAW, IF SO THEN
C UPDATE EXISTING VALIDATION RECORD ELSE WRITE
C A NEW RECORD TO THE VALIDATION FILE.
C
	IF(WIN.NE.0) THEN
	  CALL IREAD(TRABUF(TCDC),V4BUF,VLF,ST)
	  IF(ST.EQ.ERRRNF) THEN
	    VLFCNT=VLFCNT+1
	    CALL FASTSET(0,V4BUF,VFLEN)
	  ENDIF
	  IF(TRABUF(TGAMTYP).EQ.TNBR) CALL NCHKWIN(TRABUF,V4BUF,WIN)
	  CALL IWRIBF(V4BUF,VLC,BIGVLF,VLFBUF,ST)
	  IF(ST.NE.0) CALL FILERR(SFNAMES(1,VLC),3,ST,TRABUF(TSER))
	ENDIF
C
C POST TRANSACTION TO THIS DRAWS SALES TABLE THEN
C CHECK IF TRANSACTION EXPIRES THIS DRAWING
C IF NOT, THEN WRITE IT TO THE NEW CARRYOVER FILE.
C
	CALL PSTSAL(TRABUF)
	CALL DWN_FUTURE(TRABUF,SAVE)
	IF(SAVE.NE.0) THEN
	  DCFCNT=DCFCNT+1
	  CALL ISWRIT(LOGBUF,DCC,DCFBUF,ST)
	  IF(ST.NE.0) CALL FILERR(SFNAMES(1,DCC),3,ST,TRABUF(TSER))
	ENDIF
	GOTO 200
C
C SCAN DRAW FILES FOR WINNERS AND CARRYOVERS
C
300	CONTINUE
	CARRYSCAN=.FALSE.
	CALL ICLOSE(DCC,DCFBUF,ST)
	IF(ST.NE.0) CALL FILERR(SFNAMES(1,DCC),4,ST,0)
	CALL IOPEN(SFNAMES(1,DCC),DCC,LREC*2,LCDC,LSER*2-1,ST)
	IF(ST.NE.0) CALL FILERR(SFNAMES(1,DCC),1,ST,0)
	CALL ITUBSIZE(DCC,TUBSIZ)
	CALL IINIB(DCC,BIGDCF,DSIZE)
	CALL INOCHKS(DCC)
C
C
	TYPE*,IAM(),' Carryover files processing complete'
	TYPE*,IAM(),' Performing daily winner selection'
	CALL FASTSET(0,V4BUF,VFLEN)
C
C
	DO 500 I=1,FILCNT
	CALL DWN_OPNDRW(FILES(1,I),PTMF)
	CALL IOINIT(TFDB,PTMF,128*256)
C
C SCAN FILE
C
	BLOCK=0
	EOF=0
	IND=8192
410	CONTINUE
	IF(IND.GE.8157) THEN
	  BLOCK=BLOCK+1
	  IND=1
	  CALL READW(TFDB,BLOCK,TMFBUF,ST)
	  IF(ST.NE.0) THEN
	    WRITE(5,900)IAM(), (FILES(K,I),K=1,5),ST,BLOCK
	    CALL GPAUSE
	  ENDIF
	ENDIF
	IF(EOF.GT.1000) GOTO 500
C
C
	IF(TMFBUF(IND).EQ.0) THEN
	  EOF=EOF+1
	  IND=IND+LREC
	  GOTO 410
	ENDIF
C
C
	EOF=0
	TEMP=TMFBUF(IND+LREC-1)
	TYPE=I1TEMP(4)
	IF(TYPE.NE.LONE.AND.TYPE.NE.LREG) THEN
	  TYPE*,IAM(),' Bad record type > ',TYPE,' index > ',IND
	  IND=IND+LREC
	  GOTO 410
	ENDIF
C
C
	LENGTH=LREC
	IF(TYPE.EQ.LONE) THEN
	  TEMP=TMFBUF(IND+LREC*2-1)
	  TYPE=I1TEMP(4)
	  IF(TYPE.EQ.LEND) LENGTH=LREC*2
	  IF(TYPE.EQ.LTWO) LENGTH=LREC*3
	ENDIF
	CALL FASTMOV(TMFBUF(IND),LOGBUF,LENGTH)
	IND=IND+LENGTH
	CALL LOGTRA(TRABUF,LOGBUF)
	CALL PSTSAL(TRABUF)
C
C CHECK IF TRANSACTION IS A WINNER.
C
	WIN=0
	IF(TRABUF(TGAMTYP).EQ.TNBR) CALL NCHKWIN(TRABUF,V4BUF,WIN)
	IF(WIN.NE.0) THEN
	  VLFCNT=VLFCNT+1
	  CALL IWRIBF(V4BUF,VLC,BIGVLF,VLFBUF,ST)
	  IF(ST.NE.0) CALL FILERR(SFNAMES(1,VLC),3,ST,TRABUF(TSER))
	  CALL FASTSET(0,V4BUF,VFLEN)
	ENDIF
C
C CHECK IF TRANSACTION IS VALID FOR FUTURE DRAWS
C
	CALL DWN_FUTURE(TRABUF,SAVE)
	IF(SAVE.NE.0) THEN
	  DCFCNT=DCFCNT+1
	  CALL IWRIBF(LOGBUF,DCC,BIGDCF,DCFBUF,ST)
	  IF(ST.NE.0) CALL FILERR(SFNAMES(1,DCC),3,ST,TRABUF(TSER))
	ENDIF
	GOTO 410
500	CONTINUE
C
C WINNER SELECTION COMPLETED
C
	CALL ICLOSB(DCC,BIGDCF,DCFBUF,ST)
	IF(ST.NE.0) CALL FILERR(SFNAMES(1,DCC),4,ST,0)
	CALL ICLOSB(VLC,BIGVLF,VLFBUF,ST)
	IF(ST.NE.0) CALL FILERR(SFNAMES(1,VLC),4,ST,0)
	CALL ICLOSE(VLF,DUMMY,ST)
	IF(ST.NE.0) CALL FILERR(SFNAMES(1,VLF),4,ST,0)
C
C CHECK AND POST SALES/WINNER DATA TO GAME FILES
C
	CALL DWN_PSTGDF
	TYPE*,IAM(),' Posting big winners to agent file'
	CALL PSTAWN(WINTAB)
	TYPE*,IAM(),' Validation file count >> ',VLFCNT
	TYPE*,IAM(),' Carryover  file count >> ',DCFCNT
	TYPE*,IAM(),' --- DWNSEL processing completed ---'
	CALL GSTOP(GEXIT_SUCCESS)
C
C
900	FORMAT(1X,A,1X,5A4,' read error> ',I4,' block> ',I8)
	END
