C
C     FILE   : TGDOCALC.FOR
C     AUTHOR : J.H.R
C     VERSION: 01            DATE: 28 / 11 / 2000
C
C
C SHARE CALCULATION TASK FOR RESTULS GAMES ( TOTOGOLO PORTUGAL )
C
C     **************************************************************************
C
C        THIS ITEM IS THE PROPERTY OF GTECH CORPORATION, POVIDENCE, RHODE
C     ISLAND, AND CONTAINS CONFIDENTIAL AND TRADE SECRET INFORMATION. IT MAY
C     NOT BE TRANSFERRED FROM THE CUSTODY OR CONTROL OF GTECH EXCEPT AS AUTO -
C     RIZED IN WRITING BY AN OFFICER OF GTECH. NEITHER THIS ITEM NOR THE
C     INFORMATION IT CONTAINS MAY BE USED, TRANSFERRED, REPRODUCED, PUBLISHED
C     OR DISCLOSED, IN WHOLE OR IN PART, AND DIRECTLY OR INDIRECTLY, EXCEPT AS
C     EXPRESSLY AUTHORIZED BY AN OFFICER OR GTECH, PURSUANT TO WRITTEN AGREEMENT
C
C     Copyright 2000 GTECH Corporation. All Rigth Reserved
C
C     **************************************************************************
C
C THIS FUNCTION CALCULATE WINNER PRIZE AMOUNT FOR EACH DIVISION
C
C=======OPTIONS /CHECK = NOOVERFLOW /EXTEND
      SUBROUTINE TGDOCALC(GNUM, DRAW, DTG_REC, TMPPOL, UPDATE)
      IMPLICIT NONE
C
C INCLUDES DEFINITION TO CALCULATE SHARE VALUES FOR WINNERS
C
      INCLUDE 'INCLIB:SYSPARAM.DEF'
      INCLUDE 'INCLIB:SYSEXTRN.DEF'
      INCLUDE 'INCLIB:GLOBAL.DEF'
      INCLUDE 'INCLIB:RECSCF.DEF'
      INCLUDE 'INCLIB:TGLCOM.DEF'
      INCLUDE 'INCLIB:DTGREC.DEF'
      INCLUDE 'INCLIB:CONCOM.DEF'
C
C FUNCTION PARAMETERS TO CALCULATE SHARE VALUES FOR WINNERS
C
      INTEGER * 4 GNUM               ! GAME NUMBER
      INTEGER * 4 DRAW               ! DRAW NUMBER
      INTEGER * 4 TMPPOL(TGGDIV)     ! POOL CARRIED OVER
      INTEGER * 4 DTG_REC(*)         ! DRAW REGISTER
C
      LOGICAL * 4 UPDATE             ! UPDATE GAME FILE OR ONLY CALCULATE POOLS
C
C CONSTANT PARAMETERS TO CALCULATE SHARE VALUES FOR WINNERS
C
      INTEGER * 4 WDIV1              ! WINNER DIVISION 1
      INTEGER * 4 WDIV2              ! WINNER DIVISION 2
      INTEGER * 4 WDIV3              ! WINNER DIVISION 3
C
C INITIATE CONSTANT PARAMETERS TO CALCULATE SHARE VALUES FOR WINNERS
C
      PARAMETER(WDIV1 = 1)           ! WINNER DIVISION 1
      PARAMETER(WDIV2 = 2)           ! WINNER DIVISION 2
      PARAMETER(WDIV3 = 3)           ! WINNER DIVISION 3
C
C VARIABLES DEFINITION TO CALCULATE SHARE VALUES FOR WINNERS
C
      INTEGER * 4 CNTA               ! COUNTER A
      INTEGER * 4 FSTS               ! FUNCTION STATUS
      INTEGER * 4 GTYP               ! GAME TYPE
      INTEGER * 4 GIND               ! GAME INDEX
      INTEGER * 4 TOTSAL             ! TOTAL SALES AMOUNT
      INTEGER * 4 TOTPOL             ! TOTAL POOL AMOUNT FROM PREVIOUS DRAW
C
      REAL * 8 DPOOL(TGGDIV)         ! TOTAL POOLS PER DIVISION
      REAL * 8 AMTWIN                ! AMOUNT WIN
      REAL * 8 TOTPRZ                ! TOTAL PRIZE
      REAL * 8 TOTWIN                ! TOTAL SHARE COUNT WINNERS
      REAL * 8 PRIZE                 ! WIN PRIZE
C
      LOGICAL WINNER                 ! WE HAVE WINNER FOR ANY DIVISION
C
C INITIATE VARIABLES 
C
      CALL FASTMOV(DTG_REC, DTGREC, DTGLEN)
C
C READ SYSTEM FILE CONFIGURATION
C
      CALL GETSCONF(SCFREC, FSTS)
      IF(FSTS .NE. 0) CALL GSTOP(GEXIT_FATAL)
C
C GET GAME TYPE AND GAME INDEX
C
      GTYP = GNTTAB(GAMTYP, GNUM)
      GIND = GNTTAB(GAMIDX, GNUM)
C
C CHECK IF GAME TYPE AND GAME NUMBER IT'S OK
C
      IF(GTYP .NE. TTGL .OR. GIND .LT. 1 .OR. GIND .GT. NUMTGL) THEN
        TYPE *, IAM()
        TYPE *, IAM(), 'Game Type/Index Parameters Configuration Error'
        TYPE *, IAM()
	CALL GSTOP(GEXIT_FATAL)
      ENDIF
C
C READ GAME FILE CONFIGURATION FROM MEMORY ( IF NOT UPDATE )
C
      IF(UPDATE .EQ. .FALSE. .AND. DRAW .EQ. DAYHDR(GNUM)) THEN
        CALL GAMLOG(TTGL, GIND, DTGREC, TGLSTS)
        RETURN
      ENDIF
C
C INITIATE VARIABLES TO CALCULATE SHARE VALUES FOR WINNERS
C
      CALL FASTSET(0, DTGRES, 2)
      CALL FASTSET(0, DTGOSV, DTGDIV)
C
C GET OFF LINE INFORMATION ( DTGSAL(2), DTGSAL(TGGENT): OFF LINE INFORMATION )
C
      TOTSAL = DTGSAL(2) + DTGSAL(TGGENT)
      TOTPOL = DFLOAT(TOTSAL * DYN_BETUNIT) * CALPER(DTGSPR)
      DO CNTA = 1, DTGDIV
        DTGOSV(CNTA) = INT(TOTPOL * CALPER(DTGPER(CNTA)))
      ENDDO
C
C GET TOTAL SALES FOR THIS DRAW NUMBER ( INCLUDING OFF LINE INFORMATION )
C
      TOTSAL = 0
      DO CNTA = 1, TGGENT
        TOTSAL = TOTSAL + DTGSAL(CNTA)
      ENDDO
C
C GET POOL AMOUNTS FOR THIS DRAW NUMBER ( ON LINE INFORMATION )
C
      TOTPOL = DFLOAT(TOTSAL * DYN_BETUNIT) * CALPER(DTGSPR)
      DO CNTA = 1, DTGDIV
        DPOOL(CNTA) = TOTPOL * CALPER(DTGPER(CNTA))
        DPOOL(CNTA) = DPOOL(CNTA) + DFLOAT(DTGASH(CNTA) * DYN_BETUNIT)
      ENDDO
C
C GET POOL AMOUNTS ( ROLLOVER FROM EXTRA DRAWS AND FROM PREVIOUS DRAW )
C
      DO CNTA = 1, DTGDIV
        DPOOL(CNTA) = DPOOL(CNTA) + DFLOAT(DTGPOL(CNTA))   ! ROLLOVER
        DPOOL(CNTA) = DPOOL(CNTA) + DFLOAT(DTGAFD(CNTA))   ! EXTRA DRAWS
      ENDDO
C
C SET TOTAL SHARE ( TOTAL NUMBER OF WINNERS, OFF LINE VALUES SHOULD BE SET )
C
      DO CNTA = 1, DTGDIV
        DTGSHR(CNTA) = DTGSHR(CNTA) + DTGOSH(CNTA)
      ENDDO
C
C IF WE DON'T HAVE WINNER FOR ANY DIVISION 
C
      WINNER = .FALSE.
      DO CNTA = 1, DTGDIV
        IF(DTGSHR(CNTA) .NE. 0) WINNER = .TRUE.
      ENDDO
C
C IF WE DON'T HAVE WINNER GOTO DO SHARE CALCULATION PROCEDURE
C
      IF(WINNER .EQ. .FALSE.) GOTO 1000
C
C CHECH IF WE HAVE WINNERS FOR DIVISION 2, AND 3, IF NOT AMOUNT TO DIVISION 1
C
      IF(DTGSHR(WDIV2) .EQ. 0 .AND. DTGSHR(WDIV3) .EQ. 0) THEN
        TYPE *, IAM()
        TYPE *, IAM(), 'We Don''t Have Winners For Divisions 2 And 3'
        TYPE *, IAM(), 'Total Amount For These Divisions It''s Transfer'
        TYPE *, IAM(), 'To Division One'
        TYPE *, IAM()
        DPOOL(WDIV1) = DPOOL(WDIV1) + DPOOL(WDIV2) + DPOOL(WDIV3)
        DPOOL(WDIV2) = 0
        DPOOL(WDIV3) = 0
      ENDIF
C
C CHECH IF WE HAVE WINNERS FOR DIVISION 2
C
      IF(DTGSHR(WDIV2) .EQ. 0 .AND. DTGSHR(WDIV3) .NE. 0) THEN
        TYPE *, IAM()
        TYPE *, IAM(), 'We Don''t Have Winners For Divisions 2'
        TYPE *, IAM(), 'Total Amount For This Divisions It''s Transfer'
        TYPE *, IAM(), 'To Division Three'
        TYPE *, IAM()
        DPOOL(WDIV3) = DPOOL(WDIV3) + DPOOL(WDIV2)
        DPOOL(WDIV2) = 0
      ENDIF
C
C CHECH IF WE HAVE WINNERS FOR DIVISION 3
C
      IF(DTGSHR(WDIV3) .EQ. 0 .AND. DTGSHR(WDIV2) .NE. 0) THEN
        TYPE *, IAM()
        TYPE *, IAM(), 'We Don''t Have Winners For Division 3'
        TYPE *, IAM(), 'Total Amount For This Divisions It''s Transfer'
        TYPE *, IAM(), 'To Division Two'
        TYPE *, IAM()
        DPOOL(WDIV2) = DPOOL(WDIV2) + DPOOL(WDIV3)
        DPOOL(WDIV3) = 0
      ENDIF
C
C DO SHARE CALCULATION PROCEDURE ( AMOUNT / NUMBER OF WINNERS ) 
C
1000  CONTINUE
      CALL FASTSET(0, DTGSHV, DTGDIV)
      DO CNTA = 1, DTGDIV
        IF(DTGSHR(CNTA) .GT. 0) THEN
          AMTWIN = DPOOL(CNTA) / DFLOAT(DTGSHR(CNTA))
          DTGSHV(CNTA) = INT(AINT(AMTWIN) / DYN_BETUNIT)
        ENDIF
      ENDDO
C
C CHECK IF WINAMOUNT(N) LESS THAN WINAMOUNT(N - 1)
C
      DO CNTA = DTGDIV, 2, -1
        IF(DTGSHV(CNTA) .GT. DTGSHV(CNTA-1) .AND. DTGSHR(CNTA-1) .NE. 0) THEN
          TYPE   *, IAM()
          TYPE 100, IAM(), CNTA - 1, CNTA 
          TYPE   *, IAM()
          TYPE   *, IAM(), 'Results Game Combining Pools ...'
          TYPE   *, IAM()
          TOTPRZ = DPOOL(CNTA) + DPOOL(CNTA - 1)
          TOTWIN = DFLOAT(DTGSHR(CNTA - 1) + DTGSHR(CNTA))
          PRIZE  = TOTPRZ / TOTWIN
          DPOOL(CNTA)     = PRIZE * DFLOAT(DTGSHR(CNTA))
          DPOOL(CNTA - 1) = PRIZE * DFLOAT(DTGSHR(CNTA - 1))
          GOTO 1000
        ENDIF
      ENDDO
C
C SET CALCULATE POOLS 
C
      DO CNTA = 1, DTGDIV
        DTGCSP(CNTA) = INT(DPOOL(CNTA))
      ENDDO
C
C SET POOL CARRIED OVER FOR NEXT DRAW
C
C ( IF WE HAVE WINNER FOR ONE DIVISION, ROUND AMOUNT IS NOT FOR NEXT DRAW
C   SAME DIVISION, WE HAVA TO SET ZERO, AND PUT THIS AMOUNT IN SHARE REPORT
C   SCML WILL USE THIS ROUND AMOUN FOR INTERNAL USE )
C
      CALL FASTSET(0, TMPPOL, TGGDIV)
      DO CNTA = 1, DTGDIV
        TMPPOL(CNTA) = DTGCSP(CNTA) - (DTGSHV(CNTA) * DTGSHR(CNTA))
        IF(DTGSHR(CNTA) .NE. 0) TMPPOL(CNTA) = 0
      ENDDO
C
C SET RESULTS TO FUNCTION PARAMETER
C
      CALL FASTMOV(DTGREC, DTG_REC, DTGLEN)
C
C END FOR SHARE CALCULATION PROCEDURE
C
      RETURN
C
C FORMATS DEFINITION TO CALCULATE SHARE VALUES FOR WINNERS
C
100   FORMAT(X, A, 'Division ', I2.2, ' Prize Is Less Than Division ', I2.2)
C
C THIS IS THE END TO CALCULATE SHARE VALUES FOR WINNERS
C
      END
