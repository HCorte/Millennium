C
C SUBROUTINE BLDGRPDET
C $Log:   GXAFXT:[GOLS]BLDGRPDET.FOV  $
C  
C     Rev 1.0   17 Apr 1996 12:18:34   HXK
C  Release of Finland for X.25, Telephone Betting, Instant Pass Thru Phase 1
C  
C     Rev 1.0   21 Jan 1993 15:43:34   DAB
C  Initial Release
C  Based on Netherlands Bible, 12/92, and Comm 1/93 update
C  DEC Baseline
C
C ** Source - gngrpdet.for **
C
*
* ========================================================
* SUBROUTINE BLDGRPDET
*
* This subroutine will build the group detail message and
* will send it to TCP/IP.
*
* Input parameters:
*
*     OUTGRP      Int*4       Group number
*     MESTYP      Int*4       Message type
*     SUBTYP      Int*4       Message subtype
*     MSGID       Int*4       PC message identifier
*     MSGSEQ      Int*4       Message sequence number
*
* Ouput paramters:
*
*     MESLEN      Int*4       Message length
*
C=======OPTIONS /CHECK=NOOVERFLOW
	SUBROUTINE BLDGRPDET(OUTGRP,MESTYP,SUBTYP,MSGID,MSGSEQ,MESLEN)
	IMPLICIT NONE
*
	INCLUDE 'INCLIB:SYSPARAM.DEF'
	INCLUDE 'INCLIB:SYSEXTRN.DEF'
*
	INCLUDE 'INCLIB:GLOBAL.DEF'
	INCLUDE 'INCLIB:X2XCOM.DEF'
	INCLUDE 'INCLIB:CONCOM.DEF'
	INCLUDE 'INCLIB:GNSMES.DEF'
	INCLUDE 'INCLIB:X2XGRP.DEF'
*
	INTEGER*4   OUTGRP              !Group number
	INTEGER*4   MESTYP              !Message type
	INTEGER*4   SUBTYP              !Message subtype
	INTEGER*4   MSGID               !PC message identifier
	INTEGER*4   MSGSEQ              !Message sequence number
	INTEGER*4   MESLEN              !Message length
	INTEGER*4   MESS(400)           !Output buffer
	INTEGER*4   OFF                 !Data message offset
	INTEGER*4   CURTIM              !Current system time
	INTEGER*4   CNFSTN		!Configured stations
	INTEGER*4   ACTSTN		!Active stations
	INTEGER*4   I
	INTEGER*4   STN			!Station number
	INTEGER*4   LSTBYT		!Last byte offset
*
* CHECK FOR A VALID GROUP NUMBER.
*
	IF(OUTGRP.LT.1 .OR. OUTGRP.GT.X2X_NUM_GROUPS) THEN
	  MESLEN=-1
	  GOTO 8000
	ENDIF
*
* CHECK FOR A USED GROUP NUMBER.
*
	IF(X2XG_CNT(OUTGRP).EQ.0) THEN
	  MESLEN=-1
	  GOTO 8000
	ENDIF
*
* COUNT THE NUMBER OF ACTIVE STATIONS.
*
	CNFSTN=0
	ACTSTN=0
	DO 100 I=1,X2XG_CNT(OUTGRP)
	  STN=X2XG_LIST(I,OUTGRP)
	  CNFSTN=CNFSTN+1
	  IF(X2XS_TIME(STN).GE.P(ACTTIM)-30*60) THEN
	    ACTSTN=ACTSTN+1
	  ENDIF
100	CONTINUE
*
* BUILD THE DOWNLINE HEADER PORTION.
*
	CALL ISBYTE(GNHDRMES_PROTID_VAL,MESS,GNDWNMES_PROTID-1)
	CALL I4TOBUF2(MESTYP,MESS,GNDWNMES_MESTYP-1)
	CALL ISBYTE(SUBTYP,MESS,GNDWNMES_SUBTYP-1)
	CALL I4TOBUF2(MSGID,MESS,GNDWNMES_MSGID-1)
	CALL I4TOBUF2(MSGSEQ,MESS,GNDWNMES_SEQNUM-1)
	CALL I4TOBUF2(GNDWNMES_CMDDTA,MESS,GNDWNMES_DATOFF-1)
	CURTIM=P(ACTTIM)
	CALL I4TOBUF4(CURTIM,MESS,GNDWNMES_TIME-1)
	CALL ISBYTE(GNDWNMES_FLAGS_DATA,MESS,GNDWNMES_FLAGS-1)
	CALL ISBYTE(0,MESS,GNDWNMES_CMDSTS-1)
	CALL ISBYTE(0,MESS,GNDWNMES_CMDDTA-1)
	OFF=GNDWNMES_CMDDTA
*
* BUILD OUTPUT DATA MESSAGE PORTION.
*
	CALL I4TOBUF2(OUTGRP,MESS,OFF+GNGRPDET_NUMBER-1)
	CALL I4TOBUF2(CNFSTN,MESS,OFF+GNGRPDET_NUMSTN-1)
	CALL I4TOBUF2(ACTSTN,MESS,OFF+GNGRPDET_ACTSTN-1)
*
* STORE THE STATIONS ASSIGNED TO THIS GROUP INTO OUTPUT
* MESSAGE.
*
	LSTBYT=GNGRPDET_STNINF
	DO 200 I=1,CNFSTN
	  STN=X2XG_LIST(I,OUTGRP)
	  CALL I4TOBUF4(STN,MESS,OFF+LSTBYT-1)
	  LSTBYT=LSTBYT+4
200	CONTINUE
*
* STORE MESSAGE LENGTH INTO MESSAGE HEADER.
*
	MESLEN=OFF+LSTBYT-1
	CALL I4TOBUF2(MESLEN,MESS,GNDWNMES_MESLEN-1)
*
* SEND MESSAGE TO TCP/IP.
*
	CALL TCP_SNDBUF(MESS,MESLEN)
*
* PROGRAM EXIT.
*
8000	CONTINUE
	RETURN
	END
