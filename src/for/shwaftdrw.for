C
C     FILE   : SHWAFTDRW.FOR
C     AUTHOR : J.H.R
C     VERSION: 01            DATE: 08 / 11 / 2001
C
C V01 JHR INITIAL RELEASE FOR PORTUGAL PROJECT
C
C SHOW PASSIVE TICKET RETURN INFORMATION WHEN DRAW IS CLOSED ( AFTHER DRAW )
C
C THIS PROGRAM GETS INFORMATION ABOUT RETURNED TICKET WHEN DRAW IS CLOSE FOR
C PASSIVE GAMES, AND SHOW IT TO THE USER
C
C     **************************************************************************
C
C        THIS ITEM IS THE PROPERTY OF GTECH CORPORATION, POVIDENCE, RHODE
C     ISLAND, AND CONTAINS CONFIDENTIAL AND TRADE SECRET INFORMATION. IT MAY
C     NOT BE TRANSFERRED FROM THE CUSTODY OR CONTROL OF GTECH EXCEPT AS AUTO -
C     RIZED IN WRITING BY AN OFFICER OF GTECH. NEITHER THIS ITEM NOR THE
C     INFORMATION IT CONTAINS MAY BE USED, TRANSFERRED, REPRODUCED, PUBLISHED
C     OR DISCLOSED, IN WHOLE OR IN PART, AND DIRECTLY OR INDIRECTLY, EXCEPT AS
C     EXPRESSLY AUTHORIZED BY AN OFFICER OR GTECH, PURSUANT TO WRITTEN AGREEMENT
C
C     Copyright 2000 GTECH Corporation. All Rigth Reserved
C
C     **************************************************************************
C
C THIS PROGRAM SHOW INFORMATION ABOUT RETURNED TICKETS WHEN DRAW IS CLOSED
C
      OPTIONS /CHECK = NOOVERFLOW /EXT
      PROGRAM SHWAFTDRW
      IMPLICIT NONE
C
C INCLUDES DEFINITON TO SHOW RETURN TICKETS INFORMATION
C
      INCLUDE 'INCLIB:SYSPARAM.DEF'
      INCLUDE 'INCLIB:SYSEXTRN.DEF'
      INCLUDE 'INCLIB:GLOBAL.DEF'
      INCLUDE 'INCLIB:PASCOM.DEF'
C
C VARIABLES DEFINITION TO SHOW RETURN TICKETS INFORMATION
C
      INTEGER * 4 EMISIND             ! EMISSION INDEX
      INTEGER * 4 GIND                ! GAME INDEX
C
      LOGICAL SHWINF                  ! THERE IS INFO TO SHOW ( YES / NO )
C
C INITIATE VARIABLES TO SHOW RETURN TICKETS INFORMATION
C
      SHWINF = .FALSE.
C
C DISPLAY HEADER TASK PRESENTATION TO USER
C
      CALL DSP_TASK_PRESENTATION
C
C LOOP TO SEARCH IF THERE IS INFORMATION TO SHOW IT ( EMISSION INDEX )
C
      DO EMISIND = 1, PAGEMI
        DO GIND = 1, NUMPAS
C
C THIS WE HAVE INFORMATION TO SHOW IT, LETS GO TO SHOW
C
          IF(PASRETAFTAMT(EMISIND, GIND) .GT. 0) THEN
            CALL SHOW_CLOSE_DRAW_INFO(EMISIND, GIND, SHWINF)
          ENDIF
C
C END OF LOOP TO SEARCH IF THERE IS INFORMATION TO SHOW IT ( EMISSION INDEX )
C
        ENDDO
      ENDDO
C
C IF WE DON'T HAVE INFORMATION TO TO SHOW, SET ONE MESSAGE TO THE USER
C
      IF(SHWINF .EQ. .FALSE.) THEN
        TYPE *, IAM(), 'There Is Not Information In Memory To Show It'
        TYPE *, IAM()
      ENDIF
C
C PROCEDURE TO SHOW RETURN TICKETS INFORMATION ENDS OK
C
      TYPE *, IAM()
      CALL GSTOP(GEXIT_SUCCESS)
C
C THIS IS THE END TO SHOW RETURN TICKETS INFORMATION
C
      END


C ******************************************************************************
C
C     SUBROUTINE: DSP_TASK_PRESENTATION
C     AUTHOR    : J.H.R
C     VERSION   : 01            DATE: 08 / 11 / 2001
C
C ******************************************************************************
C
C PROCEDURE TO DISPLAY USER PRESENTATION TASK
C
      OPTIONS /CHECK = NOOVERFLOW /EXT
      SUBROUTINE DSP_TASK_PRESENTATION
      IMPLICIT NONE
C
C INCLUDES DEFINITION TO DISPLAY USER PRESENTATION TASK
C
      INCLUDE 'INCLIB:SYSPARAM.DEF'
      INCLUDE 'INCLIB:SYSEXTRN.DEF'
C
C CLEAR SCREEN AND DISPLAY USER PRESENTATION ( MANUAL RUN PROGRAM )
C
      CALL CLRSCR(6)
      CALL COPYRITE
      TYPE *, IAM()
      TYPE *, IAM(), '* * * * * * * * * * * * * * * * * * * * * * * * * * * *'
      TYPE *, IAM(), '*   SHOW RETURN TICKET INFORMATION FOR CLOSED DRAWS   *'
      TYPE *, IAM(), '* * * * * * * * * * * * * * * * * * * * * * * * * * * *'
      TYPE *, IAM()
C
C DON'T ALLOW RUN THIS PROGRAM IN SUBRUN MODE, ONLY MANUAL MODE
C
      IF(ISSUBPROC() .EQ. .TRUE.) THEN
        TYPE *, IAM(), 'Is Not Allowed Run This Program In Subrun Mode ...'
        TYPE *, IAM()
        CALL GSTOP(GEXIT_FATAL)
      ENDIF
C
C THIS IS THE END TO DISPLAY USER PRESENTATION TASK
C
      END


C ******************************************************************************
C
C     SUBROUTINE: SHOW_CLOSE_DRAW_INFO
C     AUTHOR    : J.H.R
C     VERSION   : 01            DATE: 08 / 11 / 2001
C
C ******************************************************************************
C
C FUNCTION SHOW RETURN INFORMATION FOR ONE EMISION ( DRAW NUMBER )
C
      OPTIONS /CHECK = NOOVERFLOW /EXT
      SUBROUTINE SHOW_CLOSE_DRAW_INFO(EMISIND, GIND, SHWINF)
      IMPLICIT NONE
C
C INCLUDES DEFINITION TO SHOW RETURN INFORMATION FOR ONE EMISION
C
      INCLUDE 'INCLIB:SYSPARAM.DEF'
      INCLUDE 'INCLIB:SYSEXTRN.DEF'
      INCLUDE 'INCLIB:GLOBAL.DEF'
      INCLUDE 'INCLIB:PASCOM.DEF'
      INCLUDE 'INCLIB:CONCOM.DEF'
C
C PARAMETERS DEFINITION TO SHOW RETURN INFORMATION FOR ONE EMISION
C
      INTEGER * 4 EMISIND               ! EMISSION INDEX
      INTEGER * 4 GIND                  ! GAME INDEX
C
      LOGICAL SHWINF                    ! THERE IS INFO TO SHOW ( YES / NO )
C
C VARIABLES DEFINITION TO SHOW RETURN INFORMATION FOR ONE EMISION
C
      INTEGER * 4 DRAW                  ! INTERNAL DRAW NUMBER
      INTEGER * 4 GNUM                  ! GAME NUMBER
      INTEGER * 4 RETAMT                ! RETURN AMOUNT
      INTEGER * 4 WEEK                  ! WEEK NUMBER
      INTEGER * 4 YEAR                  ! YEAR NUMBER
      INTEGER * 4 FSTS                  ! FUNCTION STATUS
      INTEGER * 4 WINSTS                ! WINNER STATUS
C
      CHARACTER * 07 DSPGAMTYP(NUMPAS)  ! DISPLAY GAME TYPE
      CHARACTER * 08 DSPWINSTS(2)       ! DISPLAY WINNER STATUS
C
C INITIATE CONSTANT VARIABLES SHOW RETURN INFORMATION FOR ONE EMISION
C
      DATA DSPGAMTYP / 'Clasica', 'Popular' /
      DATA DSPWINSTS / '  Return', 'Draw Day' /
C
C INITIATE VARIABLES TO SHOW RETURN INFORMATION FOR ONE EMISION
C
      WINSTS = 1
      DRAW = PASEMIS(EMISIND, GIND)
      GNUM = GTNTAB(TPAS, GIND)
      RETAMT = PASRETAFTAMT(EMISIND, GIND)
C
C CHECK IF TODAY WE HAVE DRAW, IF SO WE HAVE TO SEND TOTAL WINNER DRAW AMOUNT
C
      IF(PASTOPAYAMT(EMISIND, GIND) .GT. 0) THEN
	WINSTS = 2
        RETAMT = PASTOPAYAMT(EMISIND, GIND) - PASRETAFTAMT(EMISIND, GIND)
      ENDIF
C
C CHECK IF PASSIVE GAME INDEX IS ACTIVE OR NOT
C
      IF(GNUM .LE. 0 .OR. GNUM .GT. MAXGAM) THEN
        TYPE *, IAM(), 'Error, Passive Game Is Not Active For Index: ', GIND
        TYPE *, IAM()
        CALL GSTOP(GEXIT_FATAL)
      ENDIF
C
C CHECK IF DRAW INFORMATION IT'S GOOD INFORMATION
C
      IF(DRAW .LE. 0 .OR. DRAW .GT. DAYDRW(GNUM)) THEN
        TYPE *, IAM(), 'Error, Getting Internal Draw Number: ', DRAW
        TYPE *, IAM()
        CALL GSTOP(GEXIT_FATAL)
      ENDIF
C
C GET SANTA CASA DRAW INFORMATION ( WEEK / YEAR )
C
      CALL GETWEK(DRAW, GNUM, WEEK, YEAR, FSTS)
      IF(FSTS .NE. 0) THEN
        TYPE *, IAM(), 'Error, Getting Santa Casa Draw Number Format'
        TYPE *, IAM()
        CALL GSTOP(GEXIT_FATAL)
      ENDIF
C
C IF WE HAVE TO PUT SOME INFORMATION DISPLAY A HEADER FOR THE INFORMATION
C
      IF(SHWINF .EQ. .FALSE.) THEN
	TYPE 100, IAM()
        TYPE 200, IAM()
        TYPE *, IAM()
        SHWINF = .TRUE.
      ENDIF
C
C DISPLAY ALL INFORMATION THAT WE HAVE IN THE SYSTEM WINDOW
C
      TYPE 300, 
     *
     *          IAM(),                         ! HEADER ( TIME / TASK ) 
     *          DRAW,                          ! DRAW NUMBER
     *          WEEK,                          ! DRAW WEEK
     *          YEAR,                          ! DRAW YEAR
     *          CMONY(RETAMT, 13, BETUNIT),    ! RETURNED AMOUNT
     *          DSPGAMTYP(GIND),               ! GAME TYPE ( CLASICA / POPULAR )
     *          DSPWINSTS(WINSTS),             ! DRAW STATUS ( DRAW DAY / RET )
     *          DAYCDC                         ! DAY CDC
C
C FORMATS DEFINITION TO SHOW RETURN INFORMATION FOR ONE EMISION
C
100   FORMAT(X, A, 'Draw  Week / Year  Return Amount     Game    Status   Cdc')
200   FORMAT(X, A, 57('-'))
300   FORMAT(X, A, I4, 4X, I2.2, ' / ', I4.4, 2X, A13, 2X, A7, 2X, A8, 2X, I4)
C
C THIS IS THE END TO SHOW RETURN INFORMATION FOR ONE EMISION
C
      END

