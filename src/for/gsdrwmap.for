C
C     FILE   : GSDRWMAP.FOR
C     AUTHOR : J.H.R
C     VERSION: 01            DATE: 06 / 06 / 2001
C
C
C V02 05-DEC-2010 FRP Lotto2 Changes
C V01 JHR INITIAL RELEASE FOR PORTUGAL PROJECT
C
C GSALER ON LINE MAP, THIS PROGRAM RUNS GSALES LOADER FOR ON LINE DRAW MAPS
C
C     **************************************************************************
C
C        THIS ITEM IS THE PROPERTY OF GTECH CORPORATION, POVIDENCE, RHODE
C     ISLAND, AND CONTAINS CONFIDENTIAL AND TRADE SECRET INFORMATION. IT MAY
C     NOT BE TRANSFERRED FROM THE CUSTODY OR CONTROL OF GTECH EXCEPT AS AUTO -
C     RIZED IN WRITING BY AN OFFICER OF GTECH. NEITHER THIS ITEM NOR THE
C     INFORMATION IT CONTAINS MAY BE USED, TRANSFERRED, REPRODUCED, PUBLISHED
C     OR DISCLOSED, IN WHOLE OR IN PART, AND DIRECTLY OR INDIRECTLY, EXCEPT AS
C     EXPRESSLY AUTHORIZED BY AN OFFICER OR GTECH, PURSUANT TO WRITTEN AGREEMENT
C
C     Copyright 2000 GTECH Corporation. All Rigth Reserved
C
C     **************************************************************************
C
C THIS PROGRAM RUNS GSALES LOADERS FOR ON LINE DRAW MAP
C
C==== OPTIONS /CHECK = NOOVERFLOW /EXT
      PROGRAM GSDRWMAP
      IMPLICIT NONE
C
C INCLUDES DEFINITION TO GENERATE GSALES LOADER MAP
C
      INCLUDE 'INCLIB:SYSPARAM.DEF'
      INCLUDE 'INCLIB:SYSEXTRN.DEF'
      INCLUDE 'INCLIB:GLOBAL.DEF'
      INCLUDE 'INCLIB:CONCOM.DEF'
      INCLUDE 'INCLIB:GSALES.DEF'
C
C CONSTANT PARAMETER DEFINITION TO RUN GSALES DRAW MAP LOADER
C
      INTEGER * 4 VERSION           ! ON LINE GAMES VERSION
C
C INITIATE CONSTANT PARAMETER DEFINITION TO RUN GSALES DRAW MAP LOADER
C
      PARAMETER(VERSION = 2)        ! ON LINE GAMES VERSION
C
C VARIABLES DEFINITION TO RUN GSALES DRAW MAP LOADER
C
      INTEGER * 4 GNUM              ! GAME NUMBER
      INTEGER * 4 GIND              ! GAME INDEX
      INTEGER * 4 GTYP              ! GAME TYPE
C
      CHARACTER * 25 FILENAME       ! GSALES FILE NAME LOADER
C
C DISPLAY USER INFORMATION ( RUNNING DRAW MAP LOADER )
C
      CALL COPYRITE
      TYPE *, IAM()
      TYPE *, IAM(), 'Running Gsales Draw Map Loader'
      TYPE *, IAM()
C
C INITIATE VARIABLES TO RUN GSALES DRAW MAP LOADER
C
      FILRECCNT = 0
C
C SET GSALES DRAW MAP FILE NAME ( YYYYDDMM_DRAWMAP.FIL )
C
      WRITE(FILENAME, 100) GET_YYYYMMDD_CDC(DAYCDC)
C
C IF GSALES DRAW MAP FILE GAME EXIST, DELETE IT
C
      CALL DELETE_FILE_NAME(FILENAME)
C
C GET FREE IDENTIFICATION FILE NUMBER
C
      IDFIL = GET_FREE_IDFIL(0)
C
C OPEN YYYYDDMM_DRAWMAP.FIL ( ASCII FILE, ALTHOUGH IT'S EXTENSION IS '.FIL' )
C
      CALL OPEN_FILE_NAME(IDFIL, FILENAME)
C
C WRITE GSALES DRAW MAP HEADER
C
      CALL GSHEADER(VERSION)
C
C LOOP TO GET ACTIVE GAMES AND WRITE INFORMATION IN LOADER
C
      DO 1000 GNUM = 1, MAXGAM
C
C GET GAME TYPE AND GAME INDEX, IF IT'S NOT ACTIVE GAME, GO TO CHECK NEXT GAME 
C
        GTYP = GNTTAB(GAMTYP, GNUM)
        GIND = GNTTAB(GAMIDX, GNUM)
        IF(GTYP .LT. 1 .OR. GTYP .GT. MAXTYP) GOTO 1000
        IF(GIND .LT. 1 .OR. GIND .GT. MAXIND) GOTO 1000
C
C WRITE DRAW MAP INFORMATION IN FILE LOADER
C
        CALL GENERATE_DRAW_MAP(GNUM)
C
C END OF LOOP TO GET ACTIVE GAMES AND WRITE INFORMATION IN LOADER
C
1000    CONTINUE
C
C WRITE DRAW MAP FOOTER
C
      CALL GSFOOTER
C
C CLOSE YYYYDDMM_DRAWMAP.FIL GSALES DRAW MAP FILE
C
      CALL USRCLOS1(IDFIL)     
C
C DISPLAY MESSAGE TO USER
C
      TYPE *, IAM()
      TYPE *, IAM(), 'Generated Draw Map GSales File: ', FILENAME
      TYPE *, IAM()
C
C CREATE GSALES END FILE ( TO GSALES MEANS END OF PROCEDURE )
C
      WRITE(FILENAME, 200) GET_YYYYMMDD_CDC(DAYCDC)
      CALL CREATE_GSALES_END_FILE(FILENAME)
C 
C GSALES DRAW MAP LOADER ENDS OK
C
      CALL GSTOP(GEXIT_SUCCESS)
C
C FORMATS DEFINITION TO GENERATE GSALES LOADER DRAW MAP
C
100   FORMAT('valx:', A8, '_drawmap.fil')
200   FORMAT('valx:', A8, '_drawmap.fin')
C
C THIS IS THE END TO GENERATE GSALES LOADER DRAW MAP
C
      END


C ******************************************************************************
C
C     SUBROUTINE: GENERATE_DRAW_MAP
C     AUTHOR    : J.H.R
C     VERSION   : 01            DATE: 06 / 06 / 2001
C
C ******************************************************************************
C
C FUNCTION TO GENERATE ALL INFORMATION OF DRAWING MAP TO GSALES LOADER
C
C==== OPTIONS /CHECK = NOOVERFLOW /EXT
      SUBROUTINE GENERATE_DRAW_MAP(GNUM)
      IMPLICIT NONE
C
C INCLUDES DEFINITION TO GENERATE DRAWING MAP TO GSALES LOADER
C
      INCLUDE 'INCLIB:GLOBAL.DEF'
      INCLUDE 'INCLIB:CONCOM.DEF'
C
C PARAMETERS DEFINITION TO GENERATE DRAWING MAP TO GSALES LOADER
C
      INTEGER * 4 GNUM              ! GAME NUMBER
C
C VARIABLES DEFINITION TO GENERATE DRAWING MAP TO GSALES LOADER
C
      INTEGER * 4 FSTS              ! FUNCTION STATUS
      INTEGER * 4 DRAW              ! DRAW COUNTER
      INTEGER * 4 WEEK              ! SANTA CASA DRAW WEEK
      INTEGER * 4 YEAR              ! SANTA CASA DRAW YEAR
C
C GET ACTUAL DRAW NUMBER, IF WE DON'T HAVE DRAW FOR THE GAME, FINISH
C
      DRAW = DAYDRW(GNUM)
      IF(DRAW .LE. 0) RETURN
C
C LOOP TO GENERATE ALL DRAWING MAP TO GSALES
C
      DOWHILE(.TRUE.)
C
C GET SANTA CASA DRAW INFORMATION ( WEEK / YEAR )
C
        CALL GETWEK(DRAW, GNUM, WEEK, YEAR, FSTS)
C
C IF WE DON'T HAVE MORE INFORMTATION, FINISH
C
        IF(FSTS .NE. 0) RETURN
C
C PRINT INFORMATION IN GSALES DRAW MAP LOADER
C
        CALL PRINT_DRAW_MAP(DRAW, WEEK, YEAR, GNUM)
C
C SET DRAW NUMBER TO PRINT NEXT DRAW INFORMATION
C
        DRAW = DRAW + 1
C
C END OF LOOP TO GENERATE DRAWING MAP TO GSALES
C
      ENDDO
C
C THIS IS THE END TO GENERATE DRAWING MAP TO GSALES LOADER
C
      END


C ******************************************************************************
C
C     SUBROUTINE: PRINT_DRAW_MAP
C     AUTHOR    : J.H.R
C     VERSION   : 01            DATE: 06 / 06 / 2001
C
C ******************************************************************************
C
C FUNCTION TO PRINT DRAW MAP INFORMATION IN GSALES LOADER
C
C==== OPTIONS /CHECK = NOOVERFLOW /EXT
      SUBROUTINE PRINT_DRAW_MAP(DRAW, WEEK, YEAR, GNUM)
      IMPLICIT NONE
C
C INCLUDES DEFINITON TO PRINT DRAW MAP INFORMATION IN GSALES LOADER
C
      INCLUDE 'INCLIB:GLOBAL.DEF'
      INCLUDE 'INCLIB:CONCOM.DEF'
      INCLUDE 'INCLIB:GSALES.DEF'
C
C PARAMETERS DEFINITON TO PRINT DRAW MAP INFORMATION IN GSALES LOADER
C
      INTEGER * 4 DRAW              ! DRAW COUNTER
      INTEGER * 4 WEEK              ! SANTA CASA DRAW WEEK (SEMANA CIVIL: WW)
      INTEGER * 4 YEAR              ! SANTA CASA DRAW YEAR
      INTEGER * 4 GNUM              ! GAME NUMBER
      INTEGER * 4 CCC               ! SANTA CASA DRAW WEEK (SEMANA DO SORTEIO: CCC)
      INTEGER * 4 CCCWEK            ! FUNCTION TO GET CIVIL WEEK (WW) FROM DRAW WEEK (CCC)
C
C VARIABLES DEFINITION TO PRINT DRAW MAP INFORMATION IN GSALES LOADER
C
      INTEGER * 4 GAMETYPE          ! GAME TYPE ( PASSIVE / ON LINE )
C
C GET IF GAME TYPE IS PASSIVE OR IF IT'S ON LINE GAME
C
      GAMETYPE = 2
      IF(GNTTAB(GAMTYP, GNUM) .EQ. TPAS) GAMETYPE = 1
C
C FOR TOTOLOTO SABADO AND QUARTA, GET THE CIVIL WEEK VALUE
C (SINCE, FOR THESE GAMES, WEEK FROM GETWEK IS THE CCC VALUE; FOT THE REST, WEEK IS THE WW VALUE)
C
      CCC = WEEK  !CCC for Lotto3&4; WW otherwise
      IF(GNUM .EQ. 6 .OR. GNUM .EQ. 7) WEEK = CCCWEK(YEAR,CCC,GNUM)  !WW for Lotto3&4
C
C WRITE INFORMATION IN DRAW MAP GSALES LOADER
C
      WRITE(FILREC, 100) GAMETYPE,     ! GAME TYPE
     *                     GNUM,         ! GAME NUMBER
     *                     DRAW,         ! INTERNAL DRAW NUMBER
     *                     YEAR,         ! EXTERNAL DRAW NUMBER ( YEAR - SCML )
     *                     WEEK,         ! EXTERNAL DRAW NUMBER ( CIVIL WEEK - SCML ): WW
     *                     CCC           ! EXTERNAL DRAW NUMBER CCC ( DRAW WEEK - SCML ): CCC
C
      FILRECLNG = 1 + 2 + 6 + 4 + 2 + 3
C
C WRITE INFORMATION IN GSALES FILE AND INCREASE NUMBER OF FILE RECORDS WRITEN
C
      WRITE(IDFIL, 200) FILREC(1:FILRECLNG)
      FILRECCNT = FILRECCNT + 1
C
C FORMATS DEFINITION TO PRINT DRAW MAP INFORMATION IN GSALES LOADER
C
100   FORMAT(I1.1, I2.2, I6.6, I4.4, I2.2, I3.3)
200   FORMAT(A)
C
C THIS IS THE END TO PRINT DRAW MAP INFORMATION IN GSALES LOADER
C
      END

