C SYSBETS.FOR
C
C V01 01-AUG-90 XXX RELEASED FOR VAX
C
C+ v02 3/23/89 ws, modified entry point,multiple bonuses
C V01 01-FEB-89 XXX INITIAL RELEASE FOR SWEDEN
C+
C+
C+     SYSBETS.FTN
C+
C+     2 ENTRY POINTS:
C+     SYSDIV - CALCULATE NUMBER OF SHARES WON FOR EACH DIVISION
C+              FOR SYSTEM BET WITH MULTIPLE BONUS NUMBERS DRAWN
C+
C+     SYSPRICE - CALCULATE PRICE OF EACH SYSTEM BET
C+
C+     YOUR LINK SHOULD HAVE DUBLE PRECISION OPTION!!!!!!!!!!!!
C+
C+
C+++++++++++++++++++++++++++++++++++++++++++++++++++++++
C+
C+     SYSDIV.FTN
C+
C+     SUBROUTINE TO GET NUMBER OF SHARES FOR SYSTEM BETS
C+
C+     CALLING SEQUENCE:
C+
C+      CALL SYSDIV(BASEBET,HIGHNR,SYSTEM,MATCH,BONUS,MAXBONUS,
C+                                              BONTAB,SHARE,BONSHAR)
C+     IN -
C+     BASEBET  - BASIC BET    (6 FOR THE GAME 6/49)
C+     HIGHNR    - HIGHEST # BET  (49 FOR THE GAME 6/49)
C+                                 NEEDED ONLY IF SYSTEM.LT.BASEBET
C+     SYSTEM   - ACTUAL NUMBER BET (8 FOR SYSTEM 8)
C+     MATCH - NUMBERS OF NUMBERS MATCHED
C+     BONUS    - BONUS NUMBER FLAG (0 IF NO BONUS NUMBER,
C+                                1 IF 1 BONUS #, 2 IF 2 BONUS NRS..)
C+     MAXBONUS - MAXIMUM NUMBER OF BONUS NUMBERS THAT COULD BE
C+                MATCHED
C+     BONTAB   - TABLE OF BONUS NUMBERS DIVISIONS, BONTAB(0:BASEBET)
C+               IF(BONTAB(DIVION).NE.0) DIVION HAS A BONUS NUMBER WIN
C+               BONTAB(*,*) SHOULD BE 0 OR 1, NOT CHECKED !!!!
C+              (E.G. FOR 6/49 WITH 3+ AND 5+ DIVISIONS BONTAB SHOULD
C+               BE SET AS FOLLOWS 0,0,0,1,0,1,0 )
C+     OUT -
C+     SHARE - TABLE WITH NUMBER OF SHARES WON FOR EACH DIVISION:
C+                              MATCHES WITH NO BONUS (0:BASEBETDIV)
C+     BONSHAR     - TABLE OF BONUS DIVISIONS WON
C+                             BONSHARE(0:BASEBETDIVISION)
C+
C+
C
C
C
C+++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
C This item is the property of GTECH Corporation, Providence, Rhode
C Island, and contains confidential and trade secret information. It
C may not be transferred from the custody or control of GTECH except
C as authorized in writing by an officer of GTECH. Neither this item
C nor the information it contains may be used, transferred,
C reproduced, published, or disclosed, in whole or in part, and
C directly or indirectly, except as expressly authorized by an
C officer of GTECH, pursuant to written agreement.
C
C Copyright 1991 GTECH Corporation. All rights reserved.
C+++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
C
C=======OPTIONS /CHECK=NOOVERFLOW
	SUBROUTINE SYSDIV(BASEBET,HIGHNR,SYSTEM,MATCH,BONUS,MAXBONUS,
     *	                          BONTAB,DIVTAB,DIVBON)
	IMPLICIT NONE
C
	INCLUDE 'INCLIB:SYSPARAM.DEF'
	INCLUDE 'INCLIB:SYSEXTRN.DEF'
C
	INTEGER*4 NON_MATCH, MISSING_NONMATCH, REST_OF_NONMATCH
	INTEGER*4 MISSING_MATCH, REST_OF_MATCH, OFF, WITHBONUS
	INTEGER*4 FASTBIN, FAIL, NONMATCH, BONUS_FLAG, BET
C
	INTEGER*4 BASEBET             !BASE BET
	INTEGER*4 HIGHNR               !HIGEST NUMBER TO BET
	INTEGER*4 SYSTEM              !ACTUAL NUMBER BET
	INTEGER*4 BONUS               !BONUS NUMBER FLAG
	INTEGER*4 MAXBONUS            !MAXIMUM NR OF BONUS DRAWN
	INTEGER*4 MATCH               !NUMBER OF NUMBERS MATCHED
	INTEGER*4 DIVTAB(0:*) !NUMBER OF SHARES FOR EACH DIVISN
	INTEGER*4 DIVBON(0:*) !# OF SHARES FOR EACH DIVISN (BONUS MATCH)
	INTEGER*4 BONTAB(0:*)   !BONUS FLAG DEFINED FOR EACH DIV
C
C
	IF (BONUS.LT.0) RETURN !INVALID BONUS #
	IF (MATCH.GT.BASEBET+BONUS) RETURN !MATCHED TO MANY
C
C           SHOULD CHECK IF BONTAB(*,*) IS 0 OR 1 (NOT DONE)
C
	IF (SYSTEM.LT.BASEBET) GOTO 200
C
C     PROCESS SYSTEM BETS WITH SYSTEM BIGGER THAN BASEBET
C
	IF (MATCH.GT.SYSTEM)        RETURN !REALY BAD
	IF (MATCH+BONUS.GT.SYSTEM)  RETURN !TO MANY WINS
C
C
	DO 100, BET=0,BASEBET
C
	   DIVTAB(BET)=0
	   DIVBON(BET)=0
C
	   IF (MATCH.LT.BET) GOTO 100  !DID NOT MATCH ENOUGH
C
	   BONUS_FLAG=BONUS*BONTAB(BET)
	   NONMATCH=BASEBET-BET
	   FAIL=SYSTEM-BONUS_FLAG-MATCH
	   IF (FAIL.GE.NONMATCH) THEN
C
	      DIVTAB(BET)=FASTBIN(MATCH,BET)*FASTBIN(FAIL,NONMATCH)
	   ENDIF
	   IF (BONUS_FLAG.NE.0.AND.FAIL.GE.NONMATCH-BONUS) THEN
C
	     WITHBONUS=0
	     DO 90, OFF=1,BONUS
	        NONMATCH=BASEBET-BET-OFF
	        IF (NONMATCH.LT.0) GOTO 90
	        IF (NONMATCH.GT.FAIL) GOTO 90
	        WITHBONUS=WITHBONUS+FASTBIN(FAIL,NONMATCH)
     *	                  *FASTBIN(BONUS,OFF)
90	     CONTINUE
	     DIVBON(BET)=WITHBONUS*FASTBIN(MATCH,BET)
	   ENDIF
C
100	CONTINUE
C
	RETURN
C
C     SYSTEM BETS WITH SYSTEM .LT. BASEBET
C
200	CONTINUE
	IF (HIGHNR.LT.BASEBET+BONTAB(BONUS))  RETURN  !HIGHNR TO LOW
C                                     SYSTEM BET
C
	DO 220, BET=0,BASEBET
C
	   DIVTAB(BET)=0
	   DIVBON(BET)=0
C
	   BONUS_FLAG=BONUS*BONTAB(BET)
C
C
	   IF (BONTAB(BET).EQ.0.OR.BONUS_FLAG.NE.0) THEN
	      REST_OF_MATCH=BASEBET-MATCH
	      MISSING_MATCH=BET-MATCH
	      IF (REST_OF_MATCH.LT.0.OR.MISSING_MATCH.LT.0) GOTO 220
	      IF (REST_OF_MATCH.LT.MISSING_MATCH)           GOTO 220
	      IF (SYSTEM-MATCH-BONTAB(BET).LT.0)             GOTO 220
C
	      IF (BONUS_FLAG.EQ.0) THEN
	         REST_OF_NONMATCH=HIGHNR-BONTAB(BET)*MAXBONUS
     *	                                         -SYSTEM-REST_OF_MATCH
	      ELSE
	         REST_OF_NONMATCH=HIGHNR-SYSTEM-REST_OF_MATCH !!!!!-1
	      ENDIF
	      MISSING_NONMATCH=BASEBET-MISSING_MATCH-SYSTEM
	      IF (REST_OF_NONMATCH.LT.0)                    GOTO 220
	      IF (MISSING_NONMATCH.LT.0)                    GOTO 220
	      IF (REST_OF_NONMATCH.LT.MISSING_NONMATCH)     GOTO 220
C
C
	       IF (BONUS_FLAG.EQ.0) THEN
	          DIVTAB(BET)=FASTBIN(REST_OF_MATCH,MISSING_MATCH)
     *	              *FASTBIN(REST_OF_NONMATCH,MISSING_NONMATCH)
	       ELSE
	          DIVBON(BET)=FASTBIN(REST_OF_MATCH,MISSING_MATCH)
     *	              *FASTBIN(REST_OF_NONMATCH,MISSING_NONMATCH)
	       ENDIF
	   ELSE
C
C     GET .NON. BONUS PRIZE
C
	      REST_OF_MATCH=BASEBET-MATCH
	      MISSING_MATCH=BET-MATCH
	      IF (REST_OF_MATCH.LT.0.OR.MISSING_MATCH.LT.0) GOTO 220
	      IF (REST_OF_MATCH.LT.MISSING_MATCH)           GOTO 220
	      IF (SYSTEM-MATCH.LT.0)                        GOTO 220
C
	      REST_OF_NONMATCH=HIGHNR-MAXBONUS-SYSTEM-REST_OF_MATCH
	      MISSING_NONMATCH=BASEBET-MISSING_MATCH-SYSTEM
	      IF (REST_OF_NONMATCH.LT.0)                    GOTO 220
	      IF (MISSING_NONMATCH.LT.0)                    GOTO 210
	      IF (REST_OF_NONMATCH.LT.MISSING_NONMATCH)     GOTO 210
C
	      DIVTAB(BET)=FASTBIN(REST_OF_MATCH,MISSING_MATCH)
     *	                   *FASTBIN(REST_OF_NONMATCH,MISSING_NONMATCH)
C
210	      CONTINUE
C
C     GET BONUS PRIZE
C
C
	      WITHBONUS=0
	      DO 215, OFF=1,MAXBONUS
	         MISSING_NONMATCH=BASEBET-MISSING_MATCH-SYSTEM-OFF
	         IF (REST_OF_NONMATCH.LT.MISSING_NONMATCH) GOTO 220
	         IF (MISSING_NONMATCH.LT.0) GOTO 215
	         NON_MATCH=FASTBIN(REST_OF_NONMATCH,MISSING_NONMATCH)
	         WITHBONUS=WITHBONUS+FASTBIN(MAXBONUS,OFF)*NON_MATCH
215	      CONTINUE
C
            DIVBON(BET)=FASTBIN(REST_OF_MATCH,MISSING_MATCH)*WITHBONUS
C
C
	   ENDIF
C
220	CONTINUE
C
	RETURN
	END
C
C+++++++++++++++++++++++++++++++++++++++++++++++++++++
C     SYSPRICE.FTN
C
C     CALCULATE PRICE OF SYSTEM BETS
C+
C+    CALL SYSPRICE(BASEBET,HIGHNR,SYSTEM,PRICE)
C+    IN -
C+    BASEBET  - BASIC BET    (6 FOR THE GAME 6/49)
C+    HIGHNR    - HIGHEST # BET  (49 FOR THE GAME 6/49)
C+                                NEEDED ONLY IF SYSTEM.LT.BASEBET
C+    SYSTEM   - ACTUAL NUMBER BET (8 FOR SYSTEM 8)
C+    OUT-
C+    PRICE - PRICE AS NUMBER OF BASEBETS THAT HAVE TO BE PLAYED
C+
C=======OPTIONS /CHECK=NOOVERFLOW
	SUBROUTINE SYSPRICE(BASEBET,HIGHNR,SYSTEM,PRICE)
	IMPLICIT NONE
C
	INCLUDE 'INCLIB:SYSPARAM.DEF'
	INCLUDE 'INCLIB:SYSEXTRN.DEF'
C
	INTEGER*4 BASEBET                   !BASE BET
	INTEGER*4 HIGHNR                    !HIGH NR THAT MAY BE PLAYED
	INTEGER*4 SYSTEM                    !NUMBER OF NUMBERS BET
	INTEGER*4 PRICE                !NUMBER OF ASEBETS FOR SYSTEM BET
	INTEGER*4 TO_BET, NON_BET, FASTBIN
C
C
	IF (SYSTEM.GE.BASEBET) THEN
	   PRICE=FASTBIN(SYSTEM,BASEBET)
	   RETURN
	ENDIF
C
C     BETS WITH SYSTEM SMALLER THAN NUMBER OF BETS
C
	NON_BET=HIGHNR-SYSTEM
	TO_BET=BASEBET-SYSTEM
C
	PRICE=FASTBIN(NON_BET,TO_BET)
	RETURN
	END
