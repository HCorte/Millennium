C
C SUBROUTINE SPSREP
C
C
C V04 05-JAN-2017 SCML Save SPORTS share data into a data structure for
C                      further interface file generation
C V03 01-AUG-2009 FJG Portugal Fiscal Legislation changes
C V02 13-NOV-2003 FRP Modify for Batch2 Totobola Changes.
C
C     FILE   : SPSREP.FOR
C     AUTHOR : J.H.R
C     VERSION: 01            DATE: 14 / 12 / 2000
C
C
C REPORT FOR SHARE CALCULATION SPORTS GAMES ( -- PORTURGAL PROJECT -- )
C
C     **************************************************************************
C
C        THIS ITEM IS THE PROPERTY OF GTECH CORPORATION, POVIDENCE, RHODE
C     ISLAND, AND CONTAINS CONFIDENTIAL AND TRADE SECRET INFORMATION. IT MAY
C     NOT BE TRANSFERRED FROM THE CUSTODY OR CONTROL OF GTECH EXCEPT AS AUTO -
C     RIZED IN WRITING BY AN OFFICER OF GTECH. NEITHER THIS ITEM NOR THE
C     INFORMATION IT CONTAINS MAY BE USED, TRANSFERRED, REPRODUCED, PUBLISHED
C     OR DISCLOSED, IN WHOLE OR IN PART, AND DIRECTLY OR INDIRECTLY, EXCEPT AS
C     EXPRESSLY AUTHORIZED BY AN OFFICER OR GTECH, PURSUANT TO WRITTEN AGREEMENT
C
C     Copyright 2000 GTECH Corporation. All Rigth Reserved
C
C     **************************************************************************
C
C THIS FUNCTION PRINTS TOTOBOLA ( SPORTS ) SHARE CALCULATION REPORT
C
C SINCE V04 VERSION SAVES SPORTS SHARE DATA FOR FURTHER SPORTS SHARE
C INTERFACE FILE CREATION
C
C=======OPTIONS /CHECK = NOOVERFLOW /EXT
CV04      SUBROUTINE SPSREP(GNUM, GIND, DRAW, COPY)
      SUBROUTINE SPSREP(GNUM,GIND,DRAW,COPY,SPSHDATA)                           !V04
      IMPLICIT NONE
C
C INCLUDES DEFINITION TO PRINT SPORTS SHARE CALCULATION REPORT
C
      INCLUDE 'INCLIB:SYSPARAM.DEF'
      INCLUDE 'INCLIB:SYSEXTRN.DEF'
      INCLUDE 'INCLIB:GLOBAL.DEF'                                        
      INCLUDE 'INCLIB:RECSCF.DEF'
      INCLUDE 'INCLIB:DSPREC.DEF'
      INCLUDE 'INCLIB:CONCOM.DEF'
CV04      INCLUDE 'INCLIB:REPPRF.DEF'
      INCLUDE 'INCLIB:SPSHFIL.DEF'                                              !V04
C
C ARGUMENTS DEFINITION TO PRINT SPORTS SHARE CALCULATION REPORT
C
      INTEGER * 4  GNUM              ! GAME NUMBER
      INTEGER * 4  GIND              ! GAME INDEX
      INTEGER * 4  DRAW              ! DRAW NUMBER
      INTEGER * 4  COPY              ! NUMBER OF REPORT COPIES
C
C CONSTANTS PARAMETERS DEFINITION TO PRINT SPORTS SHARE CALCULATION REPORT
C
      INTEGER * 4 IDFIL              ! IDENTIFICATION FILE
      INTEGER * 4 REPFIL             ! IDENTIFICATION REPOT FILE
      INTEGER * 4 WDIV1              ! WINNER DIVISION 1
      INTEGER * 4 WDIV2              ! WINNER DIVISION 2
      INTEGER * 4 WDIV3              ! WINNER DIVISION 3
      INTEGER * 4 WDIV4              ! WINNER DIVISION 4
C
C INIT CONTS PARAMETERS DEFINITION TO PRINT SPORTS SHARE CALCULATION REPORT
C
      PARAMETER(IDFIL = 3)           ! IDENTIFICATION FILE
      PARAMETER(REPFIL = 8)          ! IDENTIFICATION REPOT FILE
      PARAMETER(WDIV1 = 1)           ! WINNER DIVISION 1
      PARAMETER(WDIV2 = 2)           ! WINNER DIVISION 2
      PARAMETER(WDIV3 = 3)           ! WINNER DIVISION 3
      PARAMETER(WDIV4 = 4)           ! WINNER DIVISION 4
C
C NUMBER OF CONSECUTIVE WEEKS WITHOUT DIV1 WINNERS SO THAT IT ROLLOVERS FROM
C DIV1 (CURRENT DRAW) TO DIV1 (NEXT DRAW). AFTERWARDS, IT ROLLOVERS FROM
C DIV1 (CURRENT DRAW) TO DIV2 (CURRENT DRAW), AND FROM HERE TO DIV1 (NEXT DRAW).
C
      INTEGER * 4 NUMPRN             ! WEEKS WITHOUT DIV1 WINNERS
      PARAMETER(NUMPRN = 8)
C
C COMMON DEFINITION TO PRINT SPORTS SHARE CALCULATION REPORT
C
      COMMON SCFREC                  ! SYSTEM FILE PARAMETERS REGISTER
C
C VARIABLES DEFINITION TO PRINT SPORTS SHARE CALCULATION REPORT
C
      INTEGER * 4 CNTA               ! COUNTER A
      INTEGER * 4 FSTS               ! FUNCTION STATUS
      INTEGER * 4 FDB(7)             ! FILE DESCRIPTION BLOCK
      INTEGER * 4 PAGE               ! REPORT PAGE NUMBER
      INTEGER * 4 REVNUM             ! REPORT REVISION NUMBER
      INTEGER * 4 WEEK               ! WEEK NUMBER FOR REPORT
      INTEGER * 4 YEAR               ! YEAR NUMBER FOR REPOR ( YYYY )
      INTEGER * 4 TOTSAL             ! TOTAL SALES AMOUNT
      INTEGER * 4 TOTAMTADD(SPGDIV)  ! TOTAL AMOUNT ADDED BY DIVISION
      INTEGER * 4 ROLLOVED(SPGDIV)   ! TOTAL ROLLOVED TO NEXT DRAW
      INTEGER * 4 TOTONPRZAMT        ! TOTAL ON LINE PRIZE AMOUNT
      INTEGER * 4 TOTROLPREVDRW      ! TOTAL ROLLOVED FORM PREVIOUS DRAW
      INTEGER * 4 TOTROLEXTDRW       ! TOTAL ROLLOVED FORM EXTRA DRAWS
      INTEGER * 4 TOTPOLAMT          ! TOTAL POOL AMOUNT
      INTEGER * 4 TOTADDAMT          ! TOTAL AMOUNT ADDED
      INTEGER * 4 TOTROLNEXT         ! TOTAL ROLLOVED TO NEXT DRAW
      INTEGER * 4 DPOOL(SPGDIV)      ! TOTAL POOLS PER DIVISION
      INTEGER * 4 DIVROUND(SPGDIV)   ! TOTAL ROUND BY DIVISION
      INTEGER * 4 TOTROUND           ! TOTAL ROUND
C
      CHARACTER * 12 REPNAM          ! REPORT NAME
      CHARACTER * 32 HEADER          ! REPORT HEADER TITLE
      CHARACTER * 33 TITGAMREP       ! TITLE GAME REPORT
      CHARACTER * 22 DRAW_CANCEL_MSG ! DRAW CANCEL MESSAGE DD/MM/YYYY AS HH:MM:SS
C
      REAL * 8 TOTPOL                ! TOTAL POOL AMOUNT TO THIS DRAW
C
      LOGICAL WINNER                 ! WE HAVE WINNER FOR ANY DIVISION
C
      RECORD /STCSPSHFIL/ SPSHDATA                                              !V04
C
C OPEN GAME FILE ( WHERE WE HAVE DRAW INFORMATION )
C
      CALL OPENW(IDFIL, SCFGFN(1,GNUM), 4, 0, 0, FSTS)
      CALL IOINIT(FDB, IDFIL, DSPSEC * 256)
      IF(FSTS .NE. 0) THEN
        TYPE *, IAM()
        TYPE *, IAM(), 'Error Opening File: ', (SCFGFN(CNTA, GNUM), CNTA = 1, 5)
        TYPE *, IAM()
        CALL GPAUSE
      ENDIF
C
        IF(GIND.EQ.SPTBN.OR.GIND.EQ.SPTBE) THEN                                 !V04 TOTOBOLA NORMAL AND TOTOBOLA EXTRA 1
          SPSHDATA.SPGTYP = TSPT                                                !V04 SAVE SPORTS GAME TYPE
          SPSHDATA.SPGNUM = GNUM                                                !V04 SAVE SPORTS GAME NUMBER
          SPSHDATA.SPGIND = GIND                                                !V04 SAVE SPORTS GAME INDEX
        ENDIF                                                                   !V04
C
C READ DRAW INFORMATION
C        
      CALL READW(FDB, DRAW, DSPREC, FSTS)
      IF(FSTS .NE. 0) THEN
        TYPE *, IAM()
        TYPE *, IAM(), 'Error Reading File: ', (SCFGFN(CNTA, GNUM), CNTA = 1, 5)
        TYPE *, IAM()
        CALL GPAUSE
      ENDIF                         
C
        IF(GIND.EQ.SPTBN.OR.GIND.EQ.SPTBE) THEN                                 !V04 TOTOBOLA NORMAL AND TOTOBOLA EXTRA 1
          SPSHDATA.SPDIV = DSPDIV                                               !V04 TOTAL SPORTS DIVISIONS
        ENDIF                                                                   !V04
C
C CLOSE GAME FILE
C
      CALL CLOSEFIL(FDB)
C
C OPEN REPORT SHARE CALCULATION FILE
C
      WRITE(REPNAM, 100) GIND                                         
      CALL ROPEN(REPNAM, REPFIL, FSTS)
      IF(FSTS .NE. 0) THEN
        TYPE *, IAM()
        TYPE *, IAM(), 'Error Opening File: ', REPNAM
        TYPE *, IAM()
        CALL GPAUSE
      ENDIF
C
C INITIATE TOTALS VARIABLES
C
      TOTONPRZAMT = 0
      TOTROLPREVDRW = 0
      TOTROLEXTDRW = 0
      TOTPOLAMT = 0
      TOTADDAMT = 0
      TOTROLNEXT = 0
      TOTROUND = 0
      CALL FASTSET(0, DIVROUND, SPGDIV)
C
C GET TOTAL SALES FOR THIS DRAW NUMBER
C
      TOTSAL = 0
      DO CNTA = 1, SPGENT
        TOTSAL = TOTSAL + DSPSAL(CNTA)
      ENDDO
C
C CALCULATE TOTAL SALES BY DIVISION
C
      TOTPOL = DFLOAT(TOTSAL) * CALPER(DSPSPR)
      DO CNTA = 1, DSPDIV
        DPOOL(CNTA) = INT(TOTPOL * CALPER(DSPPER(CNTA)))
        DPOOL(CNTA) = DPOOL(CNTA) + DSPASH(CNTA)
      ENDDO
C
C TALCUALTE TOTALS BY DIVISION ( ROLLOVED AND AMOUND ADDED )
C
C ( ROUND AMOUNT DO NOT GO TO NEXT DRAW NEXT DIVISION, SEE SHARE CALCULATION )
C
C NOTE: - IF THE DRAW HAS BEEN CANCELLED, ROLLOVER THE JACKPOT AMOUNT FROM THE PREVIOUS DRAW
C         DUE IT HAS NOT BEEN USED DURING THE SHARE CALCULATION PROCESS AND SHOULD BE ROOLOVER
C         TO THE NEXT DRAW
C

      DO CNTA = 1, DSPDIV
        TOTAMTADD(CNTA) = DSPAFD(CNTA) + DSPPOL(CNTA)
        ROLLOVED(CNTA) = DSPCSP(CNTA) - (DSPSHV(CNTA) * DSPSHR(CNTA))
C
        IF(DSPDCD .NE. 0) THEN  ! DRAW CANCELLED
          ROLLOVED(CNTA) = ROLLOVED(CNTA) + DSPPOL(CNTA) + DSPAFD(CNTA) ! REGULAR + EXTRA DRAWS
        ENDIF
C
        IF(DSPSHR(CNTA) .NE. 0) THEN
          DIVROUND(CNTA) = ROLLOVED(CNTA)
          ROLLOVED(CNTA) = 0
          TOTROUND = TOTROUND + DIVROUND(CNTA)
        ENDIF
      ENDDO
C
C GET WEEL / YEAR INFORMATION
C
      CALL GETWEK(DRAW, GNUM, WEEK, YEAR, FSTS)
      IF(FSTS .NE. 0) THEN
        TYPE *, IAM()
        TYPE *, IAM(), 'Error Getting Week / Year Draw Number'
        TYPE *, IAM()
        CALL GPAUSE
      ENDIF
C
        IF(GIND.EQ.SPTBN.OR.GIND.EQ.SPTBE) THEN                                 !V04 TOTOBOLA NORMAL AND TOTOBOLA EXTRA 1
          SPSHDATA.SPCCC = WEEK                                                 !V04 SAVE DRAW NUMBER
          SPSHDATA.SPYEAR = YEAR                                                !V04 SAVE DRAW YEAR
        ENDIF                                                                   !V04
C
C PRINT HEADER INFORMATION
C
      PAGE = 0
      REVNUM = 0                                                   
      WRITE(HEADER, 101)
      CALL TITLE(HEADER, REPNAM(1:8), REVNUM, REPFIL, PAGE, DAYCDC)                     
C
C SET TITLE GAME REPORT ( TOTOBOLA / TOTOBOLA EXTRAORDINARIO )
C
      WRITE(TITGAMREP, 204) '        JOGO DE TOTOBOLA NORMAL, '
      IF(DSPEXT .NE. 0) THEN 
        IF(GIND .EQ. 2) WRITE(TITGAMREP, 204) '       JOGO DE TOTOBOLA EXTRA 2, ' 
        IF(GIND .EQ. 3) WRITE(TITGAMREP, 204) '       JOGO DE TOTOBOLA EXTRA 1, ' 
      ENDIF
C
C PRINT DRAW NUMBER, WEEK NUMBER, YEAR
C
      WRITE(REPFIL, 200)
      WRITE(REPFIL, 102)
      WRITE(REPFIL, 103) TITGAMREP, WEEK, YEAR
      WRITE(REPFIL, 102)
C
C PRINT SALES BY DIVISION HEADER ( ON LINE, OFF LINE, ROLLOVERS, EXTRA DRAWS ) 
C
      WRITE(REPFIL, 200)
      WRITE(REPFIL, 102)
      WRITE(REPFIL, 104)
      WRITE(REPFIL, 105)
      WRITE(REPFIL, 106)
C
C PRINT SALES BY DIVISION ( ON LINE, OFF LINE, ROLLOVERS, EXTRA DRAWS )
C
      DO CNTA = 1, DSPDIV
        WRITE(REPFIL, 107) DSPMAT(CNTA),
     *                     CMONY(DPOOL(CNTA), 14, BETUNIT),
     *                     CMONY(DSPPOL(CNTA), 14, BETUNIT),
     *                     CMONY(DSPAFD(CNTA), 14, BETUNIT),
     *                     CMONY(DIVROUND(CNTA), 14, BETUNIT),
     *                     DISPER(DSPPER(CNTA))
C
        IF(GIND.EQ.SPTBN.OR.GIND.EQ.SPTBE) THEN                                 !V04 TOTOBOLA NORMAL AND TOTOBOLA EXTRA 1
          SPSHDATA.SPDIVNUMB(CNTA) = DSPMAT(CNTA)                               !V04 SAVE NUMBER OF EACH PRIZE DIVISION
          SPSHDATA.SPDIVPOOL(CNTA) = DPOOL(CNTA)                                !V04 SAVE TOTAL POOL VALUE OF EACH PRIZE DIVISION
          SPSHDATA.SPDIVPRJK(CNTA) = DSPPOL(CNTA)                               !V04 SAVE PREVIOUS JACKPOT AMOUNT OF EACH PRIZE DIVISION
          SPSHDATA.SPDIVEXJK(CNTA) = DSPAFD(CNTA)                               !V04 SAVE EXTRA JACKPOT AMOUNT OF EACH PRIZE DIVISION
          SPSHDATA.SPDIVRESI(CNTA) = DIVROUND(CNTA)                             !V04 SAVE TOTAL RESIDUAL VALUE OF EACH PRIZE DIVISION
          SPSHDATA.SPDIVPERC(CNTA) = DSPPER(CNTA)                               !V04 SAVE ASSIGNED PERCENTAGE OF EACH PRIZE DIVISION
        ENDIF                                                                   !V04
C
      ENDDO
C
C PRINT TOTALS POOL AMOUNT BY DIVISION HEADER
C
      WRITE(REPFIL, 108)
      WRITE(REPFIL, 109)
      WRITE(REPFIL, 110)
      WRITE(REPFIL, 124)
C
C PRINT TOTALS POOL AMOUNT BY DIVISION
C
      DO CNTA = 1, DSPDIV
        WRITE(REPFIL, 111) DSPMAT(CNTA),
     *                     CMONY(DSPCSP(CNTA), 14, BETUNIT),
     *                     CMONY(TOTAMTADD(CNTA), 14, BETUNIT),
     *                     DSPSHR(CNTA),
     *                     CMONY(DSPSHV(CNTA), 14, BETUNIT),
     *                     CMONY(ROLLOVED(CNTA), 14, BETUNIT)
C
        IF(GIND.EQ.SPTBN.OR.GIND.EQ.SPTBE) THEN                                 !V04 TOTOBOLA NORMAL AND TOTOBOLA EXTRA 1
          SPSHDATA.SPDIVTOTV(CNTA) = DSPCSP(CNTA)                               !V04 SAVE NUMBER OF EACH PRIZE DIVISION
          SPSHDATA.SPDIVPRVV(CNTA) = TOTAMTADD(CNTA)                            !V04 SAVE PREVIOUS PRIZE VALUE OF EACH PRIZE DIVISION
          SPSHDATA.SPDIVSHRQ(CNTA) = DSPSHR(CNTA)                               !V04 SAVE TOTAL PRIZED BETS OF EACH PRIZE DIVISION
          SPSHDATA.SPDIVSHRV(CNTA) = DSPSHV(CNTA)                               !V04 SAVE SHARE AMOUNT VALUE OF EACH PRIZE DIVISION
          SPSHDATA.SPDIVNXJK(CNTA) = ROLLOVED(CNTA)                             !V04 SAVE NEXT JACKPOT AMOUNT OF EACH PRIZE DIVISION
        ENDIF                                                                   !V04
C
      ENDDO
C
C PRINT HEADER INFORMATION ( PAGE NUMBER 2 )
C
      WRITE(REPFIL, 203)
      CALL TITLE(HEADER, REPNAM(1:8), REVNUM, REPFIL, PAGE, DAYCDC)                     
      WRITE(REPFIL, 200)
C
C CALCULATE TOTALS
C
      DO CNTA = 1, DSPDIV
        TOTONPRZAMT = TOTONPRZAMT + DPOOL(CNTA)
        TOTROLPREVDRW = TOTROLPREVDRW + DSPPOL(CNTA)
        TOTROLEXTDRW = TOTROLEXTDRW + DSPAFD(CNTA)
        TOTPOLAMT = TOTPOLAMT + DSPCSP(CNTA)
        TOTADDAMT = TOTADDAMT + TOTAMTADD(CNTA)
        TOTROLNEXT = TOTROLNEXT + ROLLOVED(CNTA)
      ENDDO
C
C PRINT "TOTALS" MESSAGE 
C
      WRITE(REPFIL, 201)
C
C PRINT TOTAL ON LINE SALES AND PERCENTAGE APPLIED TO WINNERS PRIZE -- SET SPECIAL MESSAG IF THE DRAW HAS BEEN CANCELLED --
C
      IF(DSPDCD .EQ. 0) THEN  
         WRITE(REPFIL, 123) CMONY(TOTSAL, 14, BETUNIT), DISPER(DSPSPR), 'PREMIOS'     ! THE DRAW HAS NOT BEEN CANCELLED
      ELSE
         WRITE(REPFIL, 123) CMONY(TOTSAL, 14, BETUNIT), DISPER(DSPSPR), 'REEMBOLSO'   ! THE DRAW HAS BEEN CANCELLED
      ENDIF
C
        IF(GIND.EQ.SPTBN.OR.GIND.EQ.SPTBE) THEN                                 !V04 TOTOBOLA NORMAL AND TOTOBOLA EXTRA 1
          SPSHDATA.SPTOTSALE = TOTSAL                                           !V04 SAVE TOTAL SALES AMOUNT
        ENDIF                                                                   !V04
C
C PRINT TOTALS AMOUNT INFORMATION
C
      WRITE(REPFIL, 112) CMONY(TOTONPRZAMT, 14, BETUNIT)
      WRITE(REPFIL, 114) CMONY(TOTROLPREVDRW, 14, BETUNIT)
      WRITE(REPFIL, 115) CMONY(TOTROLEXTDRW, 14, BETUNIT)
      WRITE(REPFIL, 116) CMONY(TOTPOLAMT, 14, BETUNIT)
      WRITE(REPFIL, 117) CMONY(TOTADDAMT, 14, BETUNIT)
      WRITE(REPFIL, 118) CMONY(TOTROLNEXT, 14, BETUNIT)
      WRITE(REPFIL, 126) CMONY(TOTROUND, 14, BETUNIT)
C
        IF(GIND.EQ.SPTBN.OR.GIND.EQ.SPTBE) THEN                                 !V04 TOTOBOLA NORMAL AND TOTOBOLA EXTRA 1
          SPSHDATA.SPTOTONPZ = TOTONPRZAMT                                      !V04 SAVE TOTAL ONLINE PRIZE AMOUNT FOR PRIZE DISTRIBUTION
          SPSHDATA.SPPREVJKT = TOTROLPREVDRW                                    !V04 SAVE PREVIOUS JACKPOT AMOUNT
          SPSHDATA.SPEXTRJKT = TOTROLEXTDRW                                     !V04 SAVE JACKPOT AMOUNT FROM EXTRA
          SPSHDATA.SPTOTPOOL = TOTPOLAMT                                        !V04 SAVE TOTAL POOL AMOUNT FOR PRIZE DISTRIBUTION
          SPSHDATA.SPPREVTOT = TOTADDAMT                                        !V04 SAVE PREVIOUS TOTAL AMOUNT
          SPSHDATA.SPNEXTJKT = TOTROLNEXT                                       !V04 SAVE NEXT JACKPOT AMOUNT
          SPSHDATA.SPTOTRESI = TOTROUND                                         !V04 SAVE TOTAL ROUNDING AMOUNT
          SPSHDATA.SPCTRFLG = 1                                                 !V04 SAVE SPSHDATA STRUCTURE LOADED SUCCESSFULLY
        ENDIF                                                                   !V04
C
C PRINT "INFORMATION" MESSAGE
C
      WRITE(REPFIL, 202)
C
C IF WE DON'T HAVE WINNER FOR ANY DIVISION
C
      WINNER = .FALSE.
      DO CNTA = 1, DSPDIV
        IF(DSPSHR(CNTA) .NE. 0) WINNER = .TRUE.
      ENDDO
C
C PRINT INFORMATION IF WE HAVE ROLLOVERS FROM OTRES DRAWS/GAMES
C
      IF(DSPROD(1) .NE. 0) WRITE(REPFIL, 119) DSPROD(1), DSPROD(2)
      IF(DSPRGT(1) .NE. 0) WRITE(REPFIL, 125) DSPRGT(1), DSPRGT(2)
      IF(WINNER .EQ. .FALSE.) GOTO 1000
C
C PRINT INFORMATION IF WE DON'T HAVE WINNERS FOR ONE DIVISION OR IF THE DRAW HAS BEEN CANCELLED
C
      IF(DSPDCD .NE. 0) THEN
        CALL SHRRPT_FRMT_CANCEL_DRAW_DATE_TIME(DSPDCD, DSPDAT(CURDRW), DRAW_CANCEL_MSG)
        WRITE(REPFIL, 128) DRAW_CANCEL_MSG
        GOTO 1000
      ENDIF
C
      IF(DSPSHR(WDIV3) .EQ. 0 .AND. DSPSHR(WDIV4) .NE. 0) THEN
        WRITE(REPFIL, 120) WDIV3, WDIV4
      ENDIF
C
      IF(DSPSHR(WDIV4) .EQ. 0 .AND. DSPSHR(WDIV3) .NE. 0) THEN
        WRITE(REPFIL, 120) WDIV4, WDIV3
      ENDIF
C
      IF(DSPSHR(WDIV3) .EQ. 0 .AND. DSPSHR(WDIV4) .EQ. 0) THEN
       WRITE(REPFIL, 121) WDIV3, WDIV4, WDIV2
      ENDIF
C
      IF(DSPSHR(WDIV2) .EQ. 0) THEN
        WRITE(REPFIL, 120) WDIV2, WDIV1
      ENDIF
C==============================================================================
C INI V03 Hardcode change. Disable this funtionality
C==============================================================================
C     IF(DSPPRN.GT.0 .AND. DSPPRN.GT.NUMPRN .AND. DSPSHR(WDIV2).NE.0) THEN
C       WRITE(REPFIL, 127) WDIV1, DSPPRN, WDIV2
C     ENDIF
C==============================================================================
C FIN V03
C==============================================================================
C
C WE DON'T HAVE WINNER FOR ANY DIVISION, PRINT MESSAGE
C
1000  CONTINUE
      IF(WINNER .EQ. .FALSE.) WRITE(REPFIL, 122)
C
C SEND REPORT TO PRINTER
C
      WRITE(REPFIL, 203)
      CALL SPOOL(REPNAM, COPY, FSTS)
C
C RETURN FOR SHARE CALCULATION REPORT
C
      RETURN
C
C FORMATS DEFINITON TO PRINT SPORTS SHARE CALCULATION REPORT
C
100   FORMAT('TB', I1, 'SHARE.REP')
C 101 FORMAT('TOTOBOLA WINNING SHARES REPORT')
101   FORMAT('RELATORIO DE PREMIOS DE TOTOBOLA')
102   FORMAT(X, /)
C 103 FORMAT(15X, A, 'WEEK ', I2.2, '/', I4.4) 
103   FORMAT(15X, A, 'SEMANA ', I2.2, '/', I4.4)
104   FORMAT(04X, 'CLASSE '               ! 03X, 'WINNING', 
     *       08X, 'VALOR TOTAL'           ! 05X, 'ON LINE AMOUNT', 
     *       10X, 'JACKPOT DA',           ! 07X, 'ROLLOVER FROM', 
     *       10X, 'JACKPOT DOS',          ! 08X, 'ROLLOVER FROM', 
     *       06X, 'ARREDONDAMENTO',       ! 08X, 'ROUND AMOUNT', 
     *       07X, 'PERCENTUAL')           ! 10X, 'ON LINE')
105   FORMAT(01X, 'DE PREMIO',            ! 02X, 'DIVISION', 
     *       08X, 'PARA PREMIOS',         ! 09X, 'FOR PRIZES', 
     *       05X, 'SEMANA ANTERIOR',      ! 08X, 'PREVIOS DRAW', 
     *       16X, 'EXTRA',                ! 10X, 'EXTRA DRAWS',
     *       20X,                         ! 09X, 'BY DIVISION', 
     *       05X, 'PARA PREMIOS')         ! 10X, 'SHARE %')
106   FORMAT(X, 107('-'))
107   FORMAT(8X, I2.2, 6X, A14, 6X, A14, 7X, A14, 6X, A14, 10X, F7.3)
108   FORMAT(X, /, X, /, X, /)
109   FORMAT(04X, 'CLASSE'                ! 03X, 'WINNING', 
     *       07X, 'VALOR TOTAL',          ! 09X, 'TOTAL POOL', 
     *       05X, 'VALOR ANTERIOR',       ! 07X, 'TOTAL AMOUNT', 
     *       04X, 'TOTAL DE',             ! 05X, '# TOTAL',
     *       09X, 'VALOR DO',             ! 04X, 'WINNING SHARE', 
     *       10X, 'JACKPOT')              ! 06X, 'ROLLOVER TO')
110   FORMAT(01X, 'DE PREMIO',            ! 02X, 'DIVISION', 
     *       18X,                         ! 12X, 'AMOUNT', 
     *       19X,                         ! 14X, 'ADDED', 
     *       03X, 'PREMIADOS',            ! 03X, 'OF SHARES', 
     *       11X, 'PREMIO'                ! 11X, 'AMOUNT', 
     *       03X, 'PROXIMA SEMANA')       ! 08X, 'NEXT DRAW')
111   FORMAT(8X, I2.2, 
     *       4X, A14, 
     *       5X, A14, 
     *       2X, I10, 
     *       3X, A14, 
     *       3X, A14)
C 112 FORMAT(20X, 'TOTAL ON LINE PRIZE AMOUNT FOR PRIZES  : ', A14)
C 113 FORMAT( -- FREE FOR USE -- )
C 114 FORMAT(20X, 'TOTAL ROLLOVER FROM PREVIOUS DRAW      : ', A14)
C 115 FORMAT(20X, 'TOTAL ROLLOVER FROM EXTRAORDINAY DRAWS : ', A14)
C 116 FORMAT(20X, 'TOTAL POOL AMOUNT                      : ', A14)
C 117 FORMAT(20X, 'TOTAL AMOUNT ADDED                     : ', A14)
C 118 FORMAT(20X, 'TOTAL ROLLOVER TO NEXT DRAW            : ', A14)
112   FORMAT(20X, 'TOTAL DESTINADO A PREMIOS   : ', A14)
C 113 FORMAT( -- FREE FOR USE -- )
114   FORMAT(20X, 'JACKPOT DA SEMANA ANTERIOR  : ', A14)
115   FORMAT(20X, 'JACKPOT DOS EXTRAORDINARIOS : ', A14)
116   FORMAT(20X, 'DESTINADO P/ PREMIOS        : ', A14)
117   FORMAT(20X, 'TOTAL VALOR ANTERIOR        : ', A14)
118   FORMAT(20X, 'JACKPOT P/ PROXIMO CONCURSO : ', A14)
C
C 123 FORMAT(20X, 'TOTAL ON LINE SALES AMOUNT  : ', A14,
C    *       05X   F6.2 ' % OF SALES ASSIGNED TO PRIZES', /)
C 119 FORMAT(10X, 'ROLOVED EXTRA MONEY FROM GAME ', I2.2, ' DRAW NUMBER ', I5.5)
C 120 FORMAT(10X, 'THERE ARE NOT WINNERS FOR DIVISION ', I2.2,  
C    *            ', POOL AMOUNT IT''S ADDED TO DIVISION ', I2.2)
C 121 FORMAT(10X, 'THERE ARE NOT WINNERS FOR DIVISION ', I2.2, ' AND ', I2.2,
C    *            ' TOTAL AMOUNT IT''S ADDED TO DIVISION ', I2.2)
C 122 FORMAT(10X, 'THERE ARE NOT WINNERS FOR ANY DIVISION')
C
119   FORMAT(10X, 'VALOR ACUMULADO DO JOGO: ', I2.2, ' CONCURSO: ', I5.5)
120   FORMAT(10X, 'NAO EXISTEM GANHADORES PARA DIVISAO 'I2.2,  
     *            ', ACUMULADO ADICIONADO PARA DIVISAO ', I2.2)
121   FORMAT(10X, 'NAO EXISTEM GANHADORES PARA DIVISAO ', I2.2, ' E ', I2.2,
     *            ' ACUMULADO ADICIONADO PARA DIVISAO ', I2.2)
122   FORMAT(10X, 'NAO EXISTEM GANHADORES PARA AS DIVISOES')
123   FORMAT(20X, 'TOTAL RECEITA               : ', A14,
     *     05X   F6.2 ' % DA RECEITA DESTINADO A', X, A, /)
124   FORMAT(X, 92('-'))
C 125 FORMAT(10X, 'ROLOVED EXTRA MONEY TO GAME ', I2.2, ' DRAW NUMBER ', I5.5)
C 126 FORMAT(20X, 'TOTAL ROUND AMOUNT                     : ', A14)
125   FORMAT(10X, 'VALOR ACUMULADO PA JOGO ', I2.2, ' CONCURSO ', I5.5)
126   FORMAT(20X, 'VALOR DE ARREDONDAMENTO     : ', A14)
127   FORMAT(10X, 'NAO EXISTEM GANHADORES PARA DIVISAO 'I2.2,
     *            ' DURANTE ',I2.2,' SEMANAS',
     *            ', ACUMULADO ADICIONADO PARA DIVISAO ', I2.2)
C
128   FORMAT(10X, 'O CONCURSO FOI ANULADO NO DIA', X, A)
C
C ANOTHERS FORMATS TO PRINT SPORTS SHARE CALCULATION REPORT
C
200   FORMAT(X, 132('-'))
201   FORMAT(X, /
     *       X, '=============', /
     *       X  'T O T A I S :', /
     *       X, '=============', /, /)
202   FORMAT(X, //
     *       X, '=====================', /
     *       X  'I N F O R M A C A O :', /
     *       X, '=====================', /, /)
203   FORMAT(X, ////)
204   FORMAT(A)
C
C THIS IS THE END FOR PRINT SPORTS SHARE CALCULATION REPORT
C
      END


C ******************************************************************************
C
C     SUBROUTINE: SHRRPT_FRMT_CANCEL_DRAW_DATE_TIME
C     AUTHOR    : J.H.R
C     VERSION   : 01            DATE: 03 / 04 / 2017
C
C ******************************************************************************
C
C FUNCTION TO FORMAT THE CANCEL EVENT DATE AND TIME
C
C==== OPTIONS /CHECK=NOOVERFLOW
      SUBROUTINE SHRRPT_FRMT_CANCEL_DRAW_DATE_TIME(ZIPDATE, DRWDATE, DRAW_CANCEL_MESSAGE)
      IMPLICIT NONE
C
      INCLUDE 'INCLIB:SYSPARAM.DEF'
      INCLUDE 'INCLIB:SYSEXTRN.DEF'
      INCLUDE 'INCLIB:DATBUF.DEF'
C
      INTEGER * 4 ZIPDATE        ! 3 BYTES FOR TIME AND 1 BYTE FOR OFFSET TO DRAW DATE
      INTEGER * 4 DRWDATE        ! DRAW DATE
C
      CHARACTER DRAW_CANCEL_MESSAGE * (*)
C
      INTEGER * 4 CANCEL_DATE    ! CANDEL DATE
      INTEGER * 4 CANCEL_TIME    ! CANCEL TIME
C
      INTEGER * 2 DATE(LDATE_LEN)
C
      WRITE(DRAW_CANCEL_MESSAGE, 100)
C
      IF(ZIPDATE .EQ. 0) RETURN
C
      CALL GET_CANCEL_EVENT_DATE_TIME(ZIPDATE, DRWDATE, CANCEL_DATE, CANCEL_TIME)
C
      DATE(VCDC) = CANCEL_DATE
C
      CALL CDATE(DATE)
C
      WRITE(DRAW_CANCEL_MESSAGE, 101) DATE(VDAY), 
     *                                DATE(VMON),
     *                                DATE(VYEAR) + 2000,
     *                                DISTIM(CANCEL_TIME)
C
100   FORMAT(22X)
101   FORMAT(I2.2, '/', I2.2, '/', I4.4, X, 'AS', X, A8)
C
      END
