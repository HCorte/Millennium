C SUBROUTINE UPDGAM
C
C V20 31-MAR-2010 RXK ePassive changes
C V19 01-DEC-2000 UXN Totogolo added.
C V18 26-JAN-2000 RXK Fixed calc. of # of boards (World Tour,cancelled ticket). 
C V17 13-OCT-1999 RXK World Tour added.
C V16 17-MAY-1999 UXN Super Triple added.
C V15 17-APR-1996 HXK Release of Finland for X.25, Telephone Betting, 
C                     Instant Pass Thru Phase 1
C V14 12-JAN-1996 HXK Fix for Double /Couple system bets
C V13 12-JAN-1996 HXK Fixed typo for SROW
C V12 15-DEC-1995 HXK Fix for system bets
C V11 23-NOV-1995 HXK Merge of post 65 stuff; changes for Double/Couple
C V10 05-MAY-1995 HXK V5 entered into database again!!!!
C V09 18-FEB-1995 HXK Changes for V5 game
C V08 01-NOV-1994 HXK Fixed bug in Bingo (did not pick up Bingo routine)
C V07 15-OCT-1994 HXK Adding /developing Bingo (15.Oct.94)
C V06 09-OCT-1993 HXK Fix for Multi week sales breakdown.
C V05 12-AUG-1993 GXA Fixed Updating of Kicker balance table 
C                     (GIND changed to KGIND).
C V04 11-AUG-1993 GXA Added updating of Ballance info by Draw table.
C V03 30-JUN-1993 GXA Added updating of AGTSPE for multi week stuff for 
C                     all games.
C V02 21-JUN-1993 SXH Added SPEDEN and RAVI
C V01 21-JAN-1993 DAB Initial Release
C                     Based on Netherlands Bible, 12/92, and Comm 1/93 update
C                     DEC Baseline
C
C SUBROUTINE TO UPDATE DETAIL GAME SALES DATA
C
C+++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
C This item is the property of GTECH Corporation, Providence, Rhode
C Island, and contains confidential and trade secret information. It
C may not be transferred from the custody or control of GTECH except
C as authorized in writing by an officer of GTECH. Neither this item
C nor the information it contains may be used, transferred,
C reproduced, published, or disclosed, in whole or in part, and
C directly or indirectly, except as expressly authorized by an
C officer of GTECH, pursuant to written agreement.
C
C Copyright 1999 GTECH Corporation. All rights reserved.
C+++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
C
C
C=======OPTIONS /CHECK=NOOVERFLOW/EXT
	SUBROUTINE UPDGAM(TRABUF,KICKER)
	IMPLICIT NONE
C
	INCLUDE 'INCLIB:SYSPARAM.DEF'
	INCLUDE 'INCLIB:SYSEXTRN.DEF'
	INCLUDE 'INCLIB:GLOBAL.DEF'
	INCLUDE 'INCLIB:DESTRA.DEF'
	INCLUDE 'INCLIB:AGTCOM.DEF'
	INCLUDE 'INCLIB:CONCOM.DEF'
	INCLUDE 'INCLIB:LTOCOM.DEF'
	INCLUDE 'INCLIB:SPTCOM.DEF'
	INCLUDE 'INCLIB:TGLCOM.DEF'
        INCLUDE 'INCLIB:KIKCOM.DEF'
        INCLUDE 'INCLIB:SCRCOM.DEF'
        INCLUDE 'INCLIB:WITCOM.DEF'
        INCLUDE 'INCLIB:TSLCOM.DEF'
	INCLUDE 'INCLIB:NBRCOM.DEF'
        INCLUDE 'INCLIB:BNGCOM.DEF'
        INCLUDE 'INCLIB:DBLCOM.DEF'
        INCLUDE 'INCLIB:CPLCOM.DEF'
        INCLUDE 'INCLIB:SSCCOM.DEF'
        INCLUDE 'INCLIB:TRPCOM.DEF'
        INCLUDE 'INCLIB:STRCOM.DEF'
        INCLUDE 'INCLIB:PASCOM.DEF'

        ! arguments
	LOGICAL  KICKER               !
C
        ! variables
	INTEGER*4  KGIND              !
	INTEGER*4  KGAM               !
	INTEGER*4  GAM                !
	INTEGER*4  POOL               !
	INTEGER*4  I                  !
	INTEGER*4  J                  !
	INTEGER*4  K                  !
	INTEGER*4  DATIND             !
	INTEGER*4  SIGN               !
	INTEGER*4  CNT                !
	INTEGER*4  KAMT               !
	INTEGER*4  AMT                !
	INTEGER*4  GIND               !
	INTEGER*4  IND                !
	INTEGER*4  NUMDRW             !
	INTEGER*4  ROW                !
	INTEGER*4  ROWAMT             !
	INTEGER*4  SYS                !
	INTEGER*4  DRAWS(MAXDRW)      !
	INTEGER*4  KDRAWS(MAXDRW)     ! Participating Draw Table for Kicker game
	INTEGER*4  TER		      ! Terminal Number to update.
	INTEGER*4  DURIND	      ! Duration Index into AGTSPE array.
        INTEGER*4  MULT               !
        INTEGER*4  OFF,OFF2           !
        INTEGER*4  EMIS
!+++++++FUNCTIONS
        INTEGER*4  GETPAGEMI
C
        LOGICAL    UPDATE             !
!               

	GAM   = TRABUF(TGAM)
	KGAM  = TRABUF(TWKGME)
	GIND  = TRABUF(TGAMIND)
	AMT   = TRABUF(TWAMT)
	KAMT  = TRABUF(TWKAMT)
	TER   = TRABUF(TTER)
	CNT   = 1
	SIGN  = 1

	IF(TRABUF(TTYP).EQ.TCAN.OR.TRABUF(TTYP).EQ.TINC) THEN
	    AMT=-AMT
	    KAMT=-KAMT
	    CNT=-1
	    SIGN=-1
	ENDIF
C
C UPDATE SALES BY DRAW TABLE
C
	IF(TRABUF(TGAMTYP).NE.TKIK) THEN
	    NUMDRW=TRABUF(TWDUR)
	    IF(NUMDRW.GT.MAXDRW) NUMDRW=MAXDRW
            IF(TRABUF(TGAMTYP).EQ.TNBR.AND.TRABUF(TWNAND).NE.0) THEN
                DRAWS(1) = TRABUF(TWBEG)-DAYDRW(GAM)+1
                DRAWS(2) = TRABUF(TWEND)-DAYDRW(GAM)+1
            ELSE
            DO I = 1, NUMDRW
                DRAWS(I)=TRABUF(TWBEG)-DAYDRW(GAM)+I
            END DO
          ENDIF
	  DO 30 I = 1, NUMDRW
	      IND=DRAWS(I)
	      IF(IND.LT.1.OR.IND.GT.MAXDRW) GOTO 30
	      DAYSAL(IND,GAM)=DAYSAL(IND,GAM)+AMT
30	  CONTINUE
	ENDIF
C
C
        IF(KICKER) THEN
            NUMDRW=TRABUF(TWKDUR)
            IF(NUMDRW.GT.MAXDRW) NUMDRW=MAXDRW
            DO I=1,NUMDRW
                KDRAWS(I)=TRABUF(TWKBEG)-DAYDRW(KGAM)+I
            END DO
            DO 60 I = 1, NUMDRW
                IND=KDRAWS(I)
                IF(IND.LT.1.OR.IND.GT.MAXDRW) GOTO 60
                DAYSAL(IND,KGAM)=DAYSAL(IND,KGAM)+KAMT
60          CONTINUE
	ENDIF
C
C
	IF(TRABUF(TGAMTYP).EQ.TLTO) GOTO  1000
	IF(TRABUF(TGAMTYP).EQ.TSPT) GOTO  2000
	IF(TRABUF(TGAMTYP).EQ.TNBR) GOTO  3000
	IF(TRABUF(TGAMTYP).EQ.TSCR) GOTO  4000
        IF(TRABUF(TGAMTYP).EQ.TWIT) GOTO  5000
        IF(TRABUF(TGAMTYP).EQ.TTSL) GOTO  6000
        IF(TRABUF(TGAMTYP).EQ.TKIK) GOTO  7000
        IF(TRABUF(TGAMTYP).EQ.TBNG) GOTO 10000
        IF(TRABUF(TGAMTYP).EQ.TDBL) GOTO 11000
        IF(TRABUF(TGAMTYP).EQ.TCPL) GOTO 12000
        IF(TRABUF(TGAMTYP).EQ.TSSC) GOTO 13000
        IF(TRABUF(TGAMTYP).EQ.TTRP) GOTO 14000
        IF(TRABUF(TGAMTYP).EQ.TSTR) GOTO 15000
        IF(TRABUF(TGAMTYP).EQ.TTGL) GOTO 16000
        IF(TRABUF(TGAMTYP).EQ.TPAS) GOTO 17000

	RETURN
C
C LOTTO GAME UPDATE
C
1000	CONTINUE
	IF(TRABUF(TWBEG).LE.LTODRW(GIND).AND.
     *	   TRABUF(TWEND).GE.LTODRW(GIND)) THEN
	   DATIND = LTODAT(CURDRW,GIND) - DAYCDC + 3
	   LTOSAL(DATIND,GIND)=LTOSAL(DATIND,GIND)+AMT
C
C UPDATE SPECIAL SALES BY GAME AND TERMINAL
C
	   IF(TRABUF(TWDUR).GE.1.AND.TRABUF(TWDUR).LE.MAXMLTD_AVL) THEN
	      DURIND = LTOMDS(TRABUF(TWDUR),GIND)
	      IF(DURIND.GT.0.AND.DURIND.LE.MAXMLTD_SEL)
     *        AGTSPE(DURIND,GAM,TER)=AGTSPE(DURIND,GAM,TER) +
     *                               (AMT*TRABUF(TWDUR))
	   ENDIF 
C
C UPDATE SALES BY DRAW BALLANCE TABLE
C
	   DO I = 1,TRABUF(TWDUR) 
	      IND = DRAWS(I)
	      IF(IND.GT.0.AND.IND.LE.MAXDRW) THEN
	         LTOBAL(IND,GIND) = LTOBAL(IND,GIND) + AMT
	      ENDIF
	   END DO
C
	ENDIF
	IF(KICKER) GOTO 7000

	RETURN
C
C SPORTS GAME UPDATE
C
2000	CONTINUE
	IF(TRABUF(TWBEG).LE.SPTDRW(GIND).AND.
     *	   TRABUF(TWEND).GE.SPTDRW(GIND)) THEN
	   DATIND = SPTDAT(CURDRW,GIND) - DAYCDC + 3
	   SPTSAL(DATIND,GIND)=SPTSAL(DATIND,GIND)+AMT
C
C UPDATE SPECIAL SALES BY GAME AND TERMINAL
C
	   IF(TRABUF(TWDUR).GE.1.AND.TRABUF(TWDUR).LE.MAXMLTD_AVL) THEN
	      DURIND = SPTMDS(TRABUF(TWDUR),GIND)
	      IF(DURIND.GT.0.AND.DURIND.LE.MAXMLTD_SEL)
     *        AGTSPE(DURIND,GAM,TER) = AGTSPE(DURIND,GAM,TER) + 
     *                                 (AMT*TRABUF(TWDUR))
	   ENDIF 
C
C UPDATE SALES BY DRAW BALLANCE TABLE
C
	   DO I = 1,TRABUF(TWDUR) 
	      IND = DRAWS(I)
	      IF(IND.GT.0.AND.IND.LE.MAXDRW) THEN
	         SPTBAL(IND,GIND) = SPTBAL(IND,GIND) + AMT
	      ENDIF
	   END DO
C
	ENDIF
	IF(KICKER) GOTO 7000

	RETURN
C
C NUMBERS GAME UPDATE
C
3000	CONTINUE
	UPDATE=.TRUE.
        IF(TRABUF(TWNAND).EQ.0) THEN
          IF(TRABUF(TWBEG).GT.NBRDRW(GIND)) UPDATE=.FALSE.
          IF(TRABUF(TWEND).LT.NBRDRW(GIND)) UPDATE=.FALSE.
        ELSE
          IF(TRABUF(TWBEG).NE.NBRDRW(GIND).AND.
     *       TRABUF(TWEND).NE.NBRDRW(GIND)) UPDATE=.FALSE.
        ENDIF
	IF(UPDATE) THEN
	  DO 3090 I=0,TRABUF(TWNBET)-1
	      POOL=TRABUF(TWNPOL1+I*3)
	      AMT=TRABUF(TWNAMT1+I*3)*SIGN
	      NBRSAL(TRACNT,POOL,GIND)=NBRSAL(TRACNT,POOL,GIND)+CNT
	      NBRSAL(DOLAMT,POOL,GIND)=NBRSAL(DOLAMT,POOL,GIND)+AMT
3090	  CONTINUE
	ENDIF

	RETURN
C
C SCORE GAME UPDATE
C
4000    CONTINUE
        SCRSAL(GIND) = SCRSAL(GIND) + AMT
        IF(TRABUF(TWSYST).EQ.FULSYS) THEN
          SYS = TRABUF(TWSYSN)
          SCRFUL(TRACNT,SYS,GIND)=SCRFUL(TRACNT,SYS,GIND)+CNT
          SCRFUL(DOLAMT,SYS,GIND)=SCRFUL(DOLAMT,SYS,GIND)+AMT
        ENDIF
C
C UPDATE SPECIAL SALES BY GAME AND TERMINAL
C
	IF(TRABUF(TWDUR).GE.1.AND.TRABUF(TWDUR).LE.MAXMLTD_AVL) THEN
	   DURIND = SCRMDS(TRABUF(TWDUR),GIND)
	   IF(DURIND.GT.0.AND.DURIND.LE.MAXMLTD_SEL)
     *     AGTSPE(DURIND,GAM,TER) = AGTSPE(DURIND,GAM,TER) + 
     *                              (AMT*TRABUF(TWDUR))
	ENDIF 
C
	RETURN
C
C WINNERS TIP GAME UPDATE
C
5000	CONTINUE
        WITSAL(GIND) = WITSAL(GIND) + AMT
        DO 5010 I = 0, TRABUF(TWNBET)-1
            ROW = TRABUF(TWWROW+I*TWWBLEN)
            ROWAMT = TRABUF(TWWAMT+I*TWWBLEN)*SIGN
            WITSBR(ROW,GIND)=WITSBR(ROW,GIND)+ROWAMT
5010    CONTINUE
C
C UPDATE SPECIAL SALES BY GAME AND TERMINAL
C
	IF(TRABUF(TWDUR).GE.1.AND.TRABUF(TWDUR).LE.MAXMLTD_AVL) THEN
	   DURIND = WITMDS(TRABUF(TWDUR),GIND)
	   IF(DURIND.GT.0.AND.DURIND.LE.MAXMLTD_SEL)
     *     AGTSPE(DURIND,GAM,TER) = AGTSPE(DURIND,GAM,TER) + 
     *                              (AMT*TRABUF(TWDUR))
	ENDIF 
C
	RETURN
C
C TOTO SELECT GAME UPDATE
C
6000	CONTINUE
        TSLSAL(GIND)=TSLSAL(GIND)+AMT
C
C UPDATE SPECIAL SALES BY GAME AND TERMINAL
C
	IF(TRABUF(TWDUR).GE.1.AND.TRABUF(TWDUR).LE.MAXMLTD_AVL) THEN
	   DURIND = TSLMDS(TRABUF(TWDUR),GIND)
	   IF(DURIND.GT.0.AND.DURIND.LE.MAXMLTD_SEL)
     *     AGTSPE(DURIND,GAM,TER) = AGTSPE(DURIND,GAM,TER) + 
     *                              (AMT*TRABUF(TWDUR))
	ENDIF 
C
	RETURN
C
C KICKER GAME UPDATE
C
7000	CONTINUE
	GAM   = TRABUF(TGAM)
	KGAM  = TRABUF(TWKGME)
	KGIND = GNTTAB(GAMIDX,KGAM)
C
C
	IF(TRABUF(TWKBEG) .LE. KIKDRW(KGIND).AND.
     *	   TRABUF(TWKEND) .GE. KIKDRW(KGIND)) THEN
	   DATIND = KIKDAT(CURDRW,KGIND)-DAYCDC+3
	   KIKSAL(DATIND,GAM,KGIND)=KIKSAL(DATIND,GAM,KGIND)+KAMT
	ENDIF
C
C UPDATE SALES BY DRAW BALLANCE TABLE
C
	DO I = 1,TRABUF(TWKDUR) 
	   IND = KDRAWS(I)
	   IF(IND.GT.0.AND.IND.LE.MAXDRW) THEN
	      KIKBAL(IND,KGIND) = KIKBAL(IND,KGIND) + KAMT
	   ENDIF
	END DO
C
	RETURN
C
C BINGO GAME UPDATE
C
10000   CONTINUE
        IF(TRABUF(TWBEG).LE.BNGDRW(GIND).AND.
     *     TRABUF(TWEND).GE.BNGDRW(GIND)) THEN
           DATIND = BNGDAT(CURDRW,GIND) - DAYCDC + 3
           BNGSAL(DATIND,GIND)=BNGSAL(DATIND,GIND)+AMT
C
C UPDATE SPECIAL SALES BY GAME AND TERMINAL
C
           IF(TRABUF(TWDUR).GE.1.AND.TRABUF(TWDUR).LE.MAXMLTD_AVL) THEN
              DURIND = BNGMDS(TRABUF(TWDUR),GIND)
              IF(DURIND.GT.0.AND.DURIND.LE.MAXMLTD_SEL)
     *        AGTSPE(DURIND,GAM,TER) = AGTSPE(DURIND,GAM,TER) +
     *                                 (AMT*TRABUF(TWDUR))
           ENDIF
C
C UPDATE SALES BY DRAW BALANCE TABLE
C
           DO I = 1,TRABUF(TWDUR)
              IND = DRAWS(I)
              IF(IND.GT.0.AND.IND.LE.MAXDRW) THEN
                 BNGBAL(IND,GIND) = BNGBAL(IND,GIND) + AMT
              ENDIF
           END DO
C
        ENDIF
        IF(KICKER) GOTO 7000
C
        RETURN
C
C DOUBLE GAME UPDATE
C
11000	CONTINUE
        DBLSAL(TRACNT,GIND) = DBLSAL(TRACNT,GIND) + 1
        DBLSAL(DOLAMT,GIND) = DBLSAL(DOLAMT,GIND) + AMT
        IF(TRABUF(TWSYST).EQ.NOSYS) THEN
           DO I = 0, TRABUF(TWNBET)-1
              ROWAMT = TRABUF(TWDBAMT+I*TWDBBLEN)*SIGN
              ROW = TRABUF(TWDBROW1+I*TWDBBLEN)
              DBLSBR(ROW,GIND) = DBLSBR(ROW,GIND)+ROWAMT/2
              ROW = TRABUF(TWDBROW2+I*TWDBBLEN)
              DBLSBR(ROW,GIND) = DBLSBR(ROW,GIND)+ROWAMT/2
           ENDDO
        ELSE
           ROWAMT = TRABUF(TWDBAMT)*SIGN
           DO 100 I = 0,TRABUF(TWNBET)-1
              DO 100 J = 0,TRABUF(TWNBET)-1
                 IF (TRABUF(TWDBROW1+I*TWDBBLEN) .EQ. 'FF'X) GOTO 100
                 IF (TRABUF(TWDBROW2+J*TWDBBLEN) .EQ. 'FF'X) GOTO 100
                 IF (TRABUF(TWDBROW1+I*TWDBBLEN) .EQ. 
     *                 TRABUF(TWDBROW2+J*TWDBBLEN)) GOTO 100
                 DBLSBR(TRABUF(TWDBROW1+I*TWDBBLEN),GIND)
     *           = DBLSBR(TRABUF(TWDBROW1+I*TWDBBLEN),GIND)+(ROWAMT/2)
                 DBLSBR(TRABUF(TWDBROW2+J*TWDBBLEN),GIND)
     *           = DBLSBR(TRABUF(TWDBROW2+J*TWDBBLEN),GIND)+(ROWAMT/2)
100	   CONTINUE
        ENDIF
C
C UPDATE SPECIAL SALES BY GAME AND TERMINAL
C
	IF(TRABUF(TWDUR).GE.1.AND.TRABUF(TWDUR).LE.MAXMLTD_AVL) THEN
	   DURIND = DBLMDS(TRABUF(TWDUR),GIND)
	   IF(DURIND.GT.0.AND.DURIND.LE.MAXMLTD_SEL)
     *     AGTSPE(DURIND,GAM,TER) = AGTSPE(DURIND,GAM,TER) + 
     *                              (AMT*TRABUF(TWDUR))
	ENDIF 
C
	RETURN
C
C COUPLE GAME UPDATE
C
12000	CONTINUE
        CPLSAL(TRACNT,GIND) = CPLSAL(TRACNT,GIND) + 1
        CPLSAL(DOLAMT,GIND) = CPLSAL(DOLAMT,GIND) + AMT
        IF(TRABUF(TWSYST).EQ.NOSYS) THEN
           DO I = 0, TRABUF(TWNBET)-1
              ROW = TRABUF(TWCPROW1+I*TWCPBLEN)
              ROWAMT = TRABUF(TWCPAMT+I*TWCPBLEN)*SIGN
              CPLSBR(ROW,GIND) = CPLSBR(ROW,GIND)+ROWAMT/2
              ROW = TRABUF(TWCPROW2+I*TWCPBLEN)+MAXCPLRW/2
              CPLSBR(ROW,GIND) = CPLSBR(ROW,GIND)+ROWAMT/2
           ENDDO
        ELSE
           ROWAMT = TRABUF(TWCPAMT)*SIGN
           DO 110 I = 0,TRABUF(TWNBET)-1
              DO 110 J = 0,TRABUF(TWNBET)-1
                 IF (TRABUF(TWCPROW1+I*TWCPBLEN) .EQ. 'FF'X) GOTO 110
                 IF (TRABUF(TWCPROW2+J*TWCPBLEN) .EQ. 'FF'X) GOTO 110
                 CPLSBR(TRABUF(TWCPROW1+I*TWCPBLEN),GIND)
     *           = CPLSBR(TRABUF(TWCPROW1+I*TWCPBLEN),GIND) + (ROWAMT/2)
                 CPLSBR(TRABUF(TWCPROW2+J*TWCPBLEN)+MAXCPLRW/2,GIND)
     *           = CPLSBR(TRABUF(TWCPROW2+J*TWCPBLEN)+MAXCPLRW/2,GIND) +
     *             (ROWAMT/2)
110	   CONTINUE
        ENDIF
C
C UPDATE SPECIAL SALES BY GAME AND TERMINAL
C
	IF(TRABUF(TWDUR).GE.1.AND.TRABUF(TWDUR).LE.MAXMLTD_AVL) THEN
	   DURIND = CPLMDS(TRABUF(TWDUR),GIND)
	   IF(DURIND.GT.0.AND.DURIND.LE.MAXMLTD_SEL)
     *     AGTSPE(DURIND,GAM,TER) = AGTSPE(DURIND,GAM,TER) + 
     *                              (AMT*TRABUF(TWDUR))
	ENDIF 
	RETURN
C
C
C SUPERSCORE GAME UPDATE
C
13000   CONTINUE
        SSCSAL(GIND) = SSCSAL(GIND) + AMT
	DURIND = 1
        AGTSPE(DURIND,GAM,TER) = AGTSPE(DURIND,GAM,TER) + 
     *                              (AMT*TRABUF(TWDUR))

	RETURN
C
C TODAY'S TRIO GAME UPDATE
C
14000	CONTINUE
        TRPSAL(TRACNT,GIND) = TRPSAL(TRACNT,GIND) + 1
        TRPSAL(DOLAMT,GIND) = TRPSAL(DOLAMT,GIND) + AMT

        OFF=0
	MULT = 1
	IF(TRABUF(TWTTMB).GT.0) MULT = MULT * TRABUF(TWTTMB)
	IF(TRABUF(TWTTMC).GT.0) MULT = MULT * TRABUF(TWTTMC)
        DO I=0,TRABUF(TWTTMA) -1
           ROW = TRABUF(TWTTBET+OFF+I)
           TRPSBR(ROW,1,GIND) = TRPSBR(ROW,1,GIND) + 
     *                          AMT*MULT/TRABUF(TWSYSN)
        ENDDO
        OFF=OFF+TRABUF(TWTTMA)
	MULT = 1
	IF(TRABUF(TWTTMA).GT.0) MULT = MULT * TRABUF(TWTTMA)
	IF(TRABUF(TWTTMC).GT.0) MULT = MULT * TRABUF(TWTTMC)
        DO I=0,TRABUF(TWTTMB) -1
           ROW = TRABUF(TWTTBET+OFF+I)
           TRPSBR(ROW,2,GIND) = TRPSBR(ROW,2,GIND) +
     *                          AMT*MULT/TRABUF(TWSYSN)
        ENDDO
        OFF=OFF+TRABUF(TWTTMB)
	MULT = 1
	IF(TRABUF(TWTTMA).GT.0) MULT = MULT * TRABUF(TWTTMA)
	IF(TRABUF(TWTTMB).GT.0) MULT = MULT * TRABUF(TWTTMB)
        DO I=0,TRABUF(TWTTMC) -1
           ROW = TRABUF(TWTTBET+OFF+I)
           TRPSBR(ROW,3,GIND) = TRPSBR(ROW,3,GIND) +
     *                          AMT*MULT/TRABUF(TWSYSN)
        ENDDO
	DURIND = 1
        AGTSPE(DURIND,GAM,TER) = AGTSPE(DURIND,GAM,TER) + 
     *                              (AMT*TRABUF(TWDUR))

	RETURN
C
C SUPER TRIPLE GAME UPDATE
C
15000	CONTINUE
        STRSAL(TRACNT,GIND) = STRSAL(TRACNT,GIND) + 1
        STRSAL(DOLAMT,GIND) = STRSAL(DOLAMT,GIND) + AMT

	ROWAMT = TRABUF(TWSTAMT)*SIGN
C
C NB! Real row amount is STRSBR(ROW,GIND)/3 !!! 
C
        OFF = TWSTBET + TRABUF(TWSTM1)
	OFF2 = OFF+TRABUF(TWSTM2)
        DO I=0,TRABUF(TWSTM1)-1
	   DO J=0,TRABUF(TWSTM2)-1
	      DO K=0,TRABUF(TWSTM3)-1
		 IF(TRABUF(TWSTBET+I).NE.TRABUF(OFF+J).AND.
     *              TRABUF(TWSTBET+I).NE.TRABUF(OFF2+K).AND.
     *              TRABUF(OFF+J).NE.TRABUF(OFF2+K)) THEN
                      ROW = TRABUF(TWSTBET+I)
                      STRSBR(ROW,GIND) = STRSBR(ROW,GIND) + ROWAMT
                      ROW = TRABUF(OFF+J)
                      STRSBR(ROW,GIND) = STRSBR(ROW,GIND) + ROWAMT
                      ROW = TRABUF(OFF2+K)
                      STRSBR(ROW,GIND) = STRSBR(ROW,GIND) + ROWAMT
		 ENDIF
	      ENDDO
	   ENDDO
	ENDDO 
	DURIND = 1
        AGTSPE(DURIND,GAM,TER) = AGTSPE(DURIND,GAM,TER) + 
     *                              (AMT*TRABUF(TWDUR))

	RETURN
C
C TOTOGOLO GAME UPDATE
C
16000	CONTINUE
	IF(TRABUF(TWBEG).LE.TGLDRW(GIND).AND.
     *	   TRABUF(TWEND).GE.TGLDRW(GIND)) THEN
	   DATIND = TGLDAT(CURDRW,GIND) - DAYCDC + 3
	   TGLSAL(DATIND,GIND)=TGLSAL(DATIND,GIND)+AMT
C
C UPDATE SPECIAL SALES BY GAME AND TERMINAL
C
	   IF(TRABUF(TWDUR).GE.1.AND.TRABUF(TWDUR).LE.MAXMLTD_AVL) THEN
	      DURIND = TGLMDS(TRABUF(TWDUR),GIND)
	      IF(DURIND.GT.0.AND.DURIND.LE.MAXMLTD_SEL)
     *        AGTSPE(DURIND,GAM,TER) = AGTSPE(DURIND,GAM,TER) + 
     *                                 (AMT*TRABUF(TWDUR))
	   ENDIF 
C
C UPDATE SALES BY DRAW BALLANCE TABLE
C
	   DO I = 1,TRABUF(TWDUR) 
	      IND = DRAWS(I)
	      IF(IND.GT.0.AND.IND.LE.MAXDRW) THEN
	         TGLBAL(IND,GIND) = TGLBAL(IND,GIND) + AMT
	      ENDIF
	   END DO
C
	ENDIF
	IF(KICKER) GOTO 7000

	RETURN
C
C EPASSIVE GAME UPDATE
C
17000   CONTINUE
        EMIS = GETPAGEMI(TRABUF(TWBEG),GIND)
        IF(EMIS.GT.0) THEN
          PASFIN(TRACNT,TRABUF(TTYP),EMIS,GIND) = PASFIN(TRACNT,TRABUF(TTYP),EMIS,GIND) + 1
          PASFIN(DOLAMT,TRABUF(TTYP),EMIS,GIND) = PASFIN(DOLAMT,TRABUF(TTYP),EMIS,GIND) + TRABUF(TWAMT)
          IF (TRABUF(TTYP).EQ.TCAN) THEN
            PASFIN(TRACNT,TWAG,EMIS,GIND) = PASFIN(TRACNT,TWAG,EMIS,GIND) - 1
            PASFIN(DOLAMT,TWAG,EMIS,GIND) = PASFIN(DOLAMT,TWAG,EMIS,GIND) - TRABUF(TWAMT)
          ENDIF
        ENDIF
        RETURN
C
	END
